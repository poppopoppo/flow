
¶ @.p_lst ◂ a'
 	∐ p_cns : { a' p_lst◂a' }
	∐ p_end : a'

§ @.rev_i { l0 l1 }
	∐ l1 ⊢ _nil◂{} .
		∎ l0
	∐. l1 ⊢ _cns◂{ hd tl } .
		_cns { hd l0 } ⊢ l0
		rev_i { l0 tl } ⊢ l0
		∎ l0
§ rev l
	_nil {} ⊢ l0
	rev_i { l0 l } ⊢ l0
	∎ l0
§ @.apnd { l0 l1 }
	$ l0 ⊢ 
	∐ _cns◂{ hd l0 } . 
		apnd { l0 l1 } ⊢ l 
		_cns { hd l } ⊢ l 
		∎ l 
	∐. _nil◂{} . 
		∎ l1 
§ @.s8_of_r64_lst_i { i rl }
	∐ rl ⊢ _cns◂{ ri rl_tl } .
		_inc i ⊢ i
		s8_of_r64_lst_i { i rl_tl } ⊢ { i s0 }
		_dec i ⊢ i
		_sts { s0 i ri } ⊢ { s0 i ri }
		∎ { i s0 }
	∐. rl ⊢ _nil◂{} .
		_mlc_s8 i ⊢ { i s0 }
		∎ { i s0 }

§ s8_of_r64_lst cl
	» 0r _ ⊢ i
	s8_of_r64_lst_i { i cl } ⊢ { i s }
	∎ s

§ @.assoc { l k }
	∐ l ⊢ _cns◂{ { k0 o0 } l } .
		∐ k'=k0' .
			∐ o0 ⊢ _some◂v0 .
				$ v0 ⊢ v0,v1
				_some v0 ⊢ o0
				_cns { { k0 o0 } l } ⊢ l
				_some v1 ⊢ r1
				∎ { l k r1 }
			∐. .
				_cns { { k0 o0 } l } ⊢ l
				_none {} ⊢ r1
				∎ { l k r1 }
		∐. .
			assoc { l k } ⊢ { l k r0 }
			_cns { { k0 o0 } l } ⊢ l
			∎ { l k r0 }
	∐. .
		_none {} ⊢ r1
		∎ { l k r1 }

§§+s8
	@.stg_ltr
	∐. "\"" stg_ltr_seq ⊢ { {} cl }
		s8_of_r64_lst cl ⊢ s
		_some s ⊢ a
		∎ a
	@.stg_ltr_seq
	∐ "\n" !| ⊢ { xx } 
		_none {} ⊢ a 
		∎ a 
	∐ "\\" stg_ltr_sp stg_ltr_seq ⊢ { {} c0 cl }
		_cns { c0 cl } ⊢ cl
		_some cl ⊢ a
		∎ a
	∐ "\"" ⊢ { {} }
		_nil {} ⊢ cl
		_some cl ⊢ a
		∎ a
	∐. _byt stg_ltr_seq ⊢ { c0 cl }
		_cns { c0 cl } ⊢ cl
		_some cl ⊢ a
		∎ a
	@.stg_ltr_sp
	∐ "\\" ⊢ { {} }
		» 92r _ ⊢ c0
		_some c0 ⊢ a0
		∎ a0
	∐ "\"" ⊢ { {} }
		» 34r _ ⊢ c0
		_some c0 ⊢ a0
		∎ a0
	∐ "n" ⊢ { {} }
		» 10r _ ⊢ c0
		_some c0 ⊢ a0
		∎ a0
	∐ "t" ⊢ { {} }
		» 9r _ ⊢ c0
		_some c0 ⊢ a0
		∎ a0
	∐. "0" ⊢ { {} }
		» 0r _ ⊢ c0
		_some c0 ⊢ a0
		∎ a0

§§+s8
	@.dgt
	∐. _byt ⊢ { c }
		» 48r _ ⊢ c0
		» 57r _ ⊢ c1
		_setge { c c0 } ⊢ { c c0 b0 }
		_setge { c1 c } ⊢ { c1 c b1 }
		∐ b0'=1r; b1'=1r .
			_some c ⊢ a
			∎ a
		∐. .
			_none {} ⊢ a
			∎ a
§§+s8
	@.ch_l
	∐. _byt ⊢ { c }
		» 97r _ ⊢ c0
		» 122r _ ⊢ c1
		_setge { c c0 } ⊢ { c c0 b0 }
		_setge { c1 c } ⊢ { c1 c b1 }
		∐ b0'=1r; b1'=1r .
			_some c ⊢ a
			∎ a
		∐. .
			_none {} ⊢ a
			∎ a

§§+s8
	@.ch_u
	∐. _byt ⊢ { c }
		» 65r _ ⊢ c0
		» 90r _ ⊢ c1
		_setge { c c0 } ⊢ { c c0 b0 }
		_setge { c1 c } ⊢ { c1 c b1 }
		∐ b0'=1r; b1'=1r .
			_some c ⊢ a
			∎ a
		∐. .
			_none {} ⊢ a
			∎ a

§§+s8
	@.ch_a
	∐ ch_u ⊢ { c }
		_some c ⊢ a
		∎ a 
	∐. ch_l ⊢ { c }
		_some c ⊢ a
		∎ a

§§+s8
	@.word
	∐. ch_a word_tl ⊢ { c0 cl }
		_cns { c0 cl } ⊢ cl
		s8_of_r64_lst cl ⊢ s
		_some s ⊢ a
		∎ a
	@.word_tl
	∐ word_hd word_tl ⊢ { c tl }
		_cns { c tl } ⊢ l
		_some l ⊢ a
		∎ a
	∐. ⊢ { }
		_nil {} ⊢ l
		_some l ⊢ a 
		∎ a
	@.word_hd
	∐ ch_a ⊢ { c }
		_some c ⊢ a
		∎ a
	∐ dgt ⊢ { c }
		_some c ⊢ a
		∎ a
	∐. "_" ⊢ { {} }
		» 95r _ ⊢ c
		_some c ⊢ a
		∎ a

§§+s8
	@.prm_word
	∐ "_" word_tl ⊢ { {} cl }
		s8_of_r64_lst cl ⊢ s
		» _^ s ⊢ s,sr 
			"_" 0' // 
		_some sr ⊢ a 
		∎ a
	∐. word ⊢ { s }
		_some s ⊢ a
		∎ a
§§ OpADL
	§§ ast
	¶ @.name
		∐ mdl_n : { _s8 name }
		∐ fst_n : _s8
	§ @.pnt_name n
		∐ n ⊢ mdl_n◂{ w n } .
			pnt_name n ⊢ s
			» _^ w,s ⊢ w,s,sr
				0' "." 1' //
			∎ sr
		∐. n ⊢ fst_n◂w .
			∎ w
	§§ reg_ptn
		¶ @.t ◂ a'
			∐ rcd : _lst◂(t◂a')
			∐ atm : a'
		∎∎
	§§ RP = reg_ptn
	¶ cst_v 
		∐ r64 : _r64 
		∐ s8 : _s8 
	¶ cst◂e'
		∐ cst_v : cst_v
		∐ cst_name : e'
	¶
		@.src r',e'
			∐ im_reg : cst◂e'
			∐ src_reg : r'
		@.dst r'
			∐ wc : {}
			∐ dst_reg : r'
		@.src_ptn r',e' = reg_ptn.t◂(src◂r'◂e')
		@.dst_ptn r' = reg_ptn.t◂(dst◂r')
	¶ SP r',e' = src_ptn◂r'◂e'
	¶ DP r' = dst_ptn◂r'
	¶
		@.mtc_atm r',e'
			∐ mtc_agl : { e' MP◂r'◂e' }
			∐ mtc_cst : cst◂e'
			∐ mtc_wc : {} 
			∐ mtc_reg : r' 
		@.MP r',e' = reg_ptn.t◂(mtc_atm◂r'◂e')
	¶
		@.lc_code r',e'
			∐ jmp : { e' SP◂r'◂e' }
			∐ app_jmp : { src◂r'◂e' SP◂r'◂e' }
			∐ seq : { lc_line◂r'◂e' lc_code◂r'◂e' }
			∐ mtc : lc_mtc◂r'◂e'
			∐ op_id : { SP◂r'◂e' lc_id_line◂r'◂e' }
			∐ ret : SP◂r'◂e'
			∐ exn : {}
		@.lc_mtc r',e' = p_lst◂{ _lst◂(mtc_op◂r'◂e') mtc_etr◂r'◂e' }
		@.lc_id_mtc r',e' = p_lst◂{ MP◂r'◂e' _lst◂(mtc_op◂r'◂e') mtc_etr◂r'◂e' }
		@.lc_id_line r',e'
			∐ id_mtc : lc_id_mtc◂r'◂e'
			∐ id_line : { _lst◂(DP◂r') lc_code◂r'◂e' }
		@.lc_line r',e'
			∐ op_name : { e' SP◂r'◂e' DP◂r' }
			∐ op_app : { src◂r'◂e' SP◂r'◂e' DP◂r' }
			∐ op_exp_cst : { cst◂e' SP◂r'◂e' DP◂r' }
			∐ op_exp_s8 : { _lst◂(op_exp_s8◂e') _lst◂(src◂r'◂e') _lst◂(dst◂r') }
		@.mtc_etr r',e'
			∐ mtc_rmf : lc_mtc◂r'◂e'
			∐ mtc_id_rmf : { SP◂r'◂e' lc_id_mtc◂r'◂e' }
			∐ mtc_code : lc_code◂r'◂e'
		@.mtc_op r',e'
			∐ mtc_id : { SP◂r'◂e' MP◂r'◂e' }
			∐ mtc_test : { test_var◂r'◂e' test_var◂r'◂e' }
		@.test_var r',e'
			∐ test_cst : cst◂e'
			∐ test_var : r'
		@.op_exp_s8 e' 
			∐ exp_s8_s8 : _s8
			∐ exp_s8_cst : cst◂e'
			∐ exp_s8_idx : { exp_s8_t _r64 }
			∐ exp_s8_for : { _r64 _s8 }
		@.exp_s8_t
			∐ exp_fmt_s : {}
			∐ exp_fmt_d : {}
			∐ exp_fmt_x : {}
			∐ exp_fmt_p : {}
	¶ @.type
		∐ type_rcd : _lst◂type
		∐ imp : { type type }
		∐ app : { type type }
		∐ type_name : name
		∐ type_var : _s8
	¶ 
		@.grm_etr
			∐ grm_cnc : grm_cnc
			∐ grm_act : grm_act
		@.grm_cnc
			∐ grm_cnc_seq : { _opn◂_s8 rle_flg grm_rle _opn◂grm_cnc grm_cnc }
			∐ grm_cnc_end : { _opn◂_s8 rle_flg grm_rle _opn◂grm_cnc }
		@.grm_act
			∐ grm_act_seq : { DP◂_s8 lc_code◂_s8◂name rle_flg grm_rle _opn◂{ DP◂_s8 grm_act } grm_act }
			∐ grm_act_end : { DP◂_s8 lc_code◂_s8◂name rle_flg grm_rle _opn◂{ DP◂_s8 grm_act } }
		@.rle_flg
			∐ lex : {}
			∐ synt : {}
			∐ line : {}
		@.grm_rle
			∐ grm_ptn_cut : grm_rle_cut
			∐ grm_ptn_seq : { grm_ptn grm_rle }
			∐ grm_ptn_end : {}
		@.grm_rle_cut
			∐ grm_rle_cut_seq : { grm_ptn grm_rle_cut }
			∐ grm_rle_cut_end : {}
		@.grm_ptn
			∐ grm_lst : grm_atm
			∐ grm_atm : grm_atm
		@.grm_atm
			∐ grm_name : name
			∐ grm_s8 : _s8
	¶
		@.file = _lst◂glb_etr
		@.glb_etr
			∐ mdl_etr : { _s8 mdl_etr }
			∐ grm_etr : _lst◂{ _s8 grm_etr }
			∐ lc_etr : { _s8 lc_etr }
			∐ lc_qlq_etr : _lst◂{ _s8 DP◂_s8 lc_code◂_s8◂name }
			∐ dt_etr : { _s8 dt_etr }
			∐ dt_qlq_etr : _lst◂{ _s8 dt_etr }
		@.mdl_etr
			∐ mdl_def : _lst◂glb_etr
			∐ mdl_ln : name
		@.lc_etr
			∐ lc_def : { DP◂_s8 lc_code◂_s8◂name }
			∐ lc_ln : cst◂name
			∐ lc_cst : lc_code◂_s8◂name
		@.dt_etr
			∐ dt_def : _lst◂{ _s8 type }
			∐ dt_ln : type
				
	§ pnt_cst c
		$ c ⊢
		∐ cst_v◂v . 
			$ v ⊢
			∐ r64◂x .
				» "??r" _ ⊢ sr
				∎ sr
			∐. s8◂s .
				» ".." _ ⊢ sr
				∎ sr
		∐. cst_name◂n .
			pnt_name n ⊢ sr
			∎ sr
	§ @.pnt_mtc_ptn mp 
		$ mp ⊢
		∐ RP.rcd◂l .
			pnt_mtc_ptn_lst l ⊢ sl 
				» _^ sl ⊢ sl,sr
					"{ " 0' "}" //
				∎ sr
		∐. RP.atm◂a .
			$ a ⊢ 
			∐ mtc_cst◂c .
				pnt_cst c ⊢ sc
				» _^ sc ⊢ sc,sr
					"%[ " 0' " ] " // 
				∎ sr
			∐ mtc_agl◂{ n mp } .
				pnt_mtc_ptn mp ⊢ smp 
				pnt_name n ⊢ sn 
				» _^ sn,smp ⊢ sn,smp,sr
					0' "◂" 1' // 
				∎ sr
			∐ mtc_reg◂w . 
				» _^ w ⊢ w,sr
					"%" 0' // 
				∎ sr 
			∐. mtc_wc◂{} . 
				» "_" _ ⊢ sr
				∎ sr 
		@.pnt_mtc_ptn_lst l
			$ l ⊢
			∐ _cns◂{ hd l } .
				pnt_mtc_ptn hd ⊢ shd 
				pnt_mtc_ptn_lst l ⊢ sl
				» _^ shd,sl ⊢ shd,sl,sr 
					0' " " 1' //
				∎ sr
			∐. _nil◂{} .
				» "" _ ⊢ sr
				∎ sr
	§ @.pnt_src_ptn sp
			∐ sp ⊢ RP.rcd◂l .
				pnt_src_ptn_lst l ⊢ sl
				» _^ sl ⊢ sl,sr
					"{ " 0' "}" //
				∎ sr
			∐. sp ⊢ RP.atm◂a .
				∐ a ⊢ im_reg◂c .
					pnt_cst c ⊢ sc
					» _^ sc ⊢ sc,sr
						"%[ " 0' " ] " // 
					∎ sr
				∐. a ⊢ src_reg◂w .
					∎ w
		@.pnt_src_ptn_lst l
			∐ l ⊢ _cns◂{ hd l } .
				pnt_src_ptn hd ⊢ shd 
				pnt_src_ptn_lst l ⊢ sl
				» _^ shd,sl ⊢ shd,sl,sr 
					0' " " 1' //
				∎ sr
			∐. l ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ sr
		@.pnt_dst_ptn p
			∐ p ⊢ RP.rcd◂l .
				pnt_dst_ptn_lst l ⊢ sl
				» _^ sl ⊢ sl,sr
					"{ " 0' "}" //
				∎ sr
			∐. p ⊢ RP.atm◂a .
				∐ a ⊢ wc◂{} .
					» "_ " _ ⊢ sr
					∎ sr
				∐. a ⊢ dst_reg◂w .
					∎ w
		@.pnt_dst_ptn_lst l
			∐ l ⊢ _cns◂{ hd l } .
				pnt_dst_ptn hd ⊢ shd
				pnt_dst_ptn_lst l ⊢ sl
				» _^ shd,sl ⊢ shd,sl,sr
					0' " " 1' //
				∎ sr
			∐. l ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ sr
	§ @.pnt_lc_code { dn lc }
			$ lc ⊢ 
			∐ jmp◂{ n s } . 
				pnt_name n ⊢ sn 
				pnt_src_ptn s ⊢ ss 
				» _^ dn,sn,ss ⊢ dn,sn,ss,sr 
					0'*"\t" 1' " " 2' " ⊢|\n" // 
				∎ { dn sr } 
			∐ app_jmp◂{ f x } . 
				RP.atm f ⊢ f 
				pnt_src_ptn f ⊢ sf 
				pnt_src_ptn x ⊢ sx 
				» _^ dn,sf,sx ⊢ dn,sf,sx,sr 
					0'*"\t" "◂" 1' "," 2' " ⊢|\n" // 
					∎ { dn sr } 
			∐ seq◂{ ll lc } .
				pnt_lc_line { dn ll } ⊢ { dn sll }
				pnt_lc_code { dn lc } ⊢ { dn slc }
				» _^ sll,slc ⊢ sll,slc,sr
					0'
					1' //
				∎ { dn sr }
			∐ mtc◂m .
				pnt_lc_mtc { dn m } ⊢ { dn sm }
				∎ { dn sm }
			∐ op_id◂{ s m } . 
				pnt_src_ptn s ⊢ ss 
				$ m ⊢ 
				∐ id_line◂{ d lc } . 
					pnt_dst_ptn_lst d ⊢ sd 
					pnt_lc_code { dn lc } ⊢ { dn slc }
					» _^ dn,ss,sd,slc ⊢ dn,ss,sd,slc,sr 
						0'*"\t" "$ " 1' " ⊢ " 2' "\n" 
						3' // 
					∎ { dn sr } 
				∐. id_mtc◂m . 
					pnt_lc_id_mtc { dn m } ⊢ { dn sm } 
					» _^ dn,ss,sm ⊢ dn,ss,sm,sr 
						0'*"\t" "$ " 1' " ⊢\n" 
						2' //
					∎ { dn sr } 
			∐ ret◂sp .
				» _^ dn ⊢ dn,s
					0'*"\t" "∎ ..\n" //
				∎ { dn s }
			∐. exn◂{} .
				» _^ dn ⊢ dn,sr
					0'*"\t" "∎|\n" //
				∎ { dn sr }
		@.pnt_lc_line { dn o }
			∐ o ⊢ op_name◂{ n s d } .
				pnt_src_ptn s ⊢ ss
				pnt_dst_ptn d ⊢ sd
				pnt_name n ⊢ sn
				» _^ dn,sn,ss,sd ⊢ dn,sn,ss,sd,sr
					0'*"\t" 1' " " 2' " ⊢ " 3' "\n" //
				∎ { dn sr }
			∐ o ⊢ op_app◂{ f x y } .
				RP.atm f ⊢ fr
				pnt_src_ptn fr ⊢ sf
				pnt_src_ptn x ⊢ ss
				pnt_dst_ptn y ⊢ sd
				» _^ dn,sf,ss,sd ⊢ dn,sf,ss,sd,sr 
					0'*"\t" "◂ " 1' "," 2' " ⊢ " 3' "\n" //
				∎ { dn sr }
			∐ o ⊢ op_exp_cst◂{ c s d } .
				pnt_src_ptn s ⊢ ss
				pnt_dst_ptn d ⊢ sd
				pnt_cst c ⊢ sc
				» _^ dn,sc,ss,sd ⊢ dn,sc,ss,sd,sr
					0'*"\t" "» " 1' " " 2' " ⊢ " 3' "\n" //
				∎ { dn sr }
			∐. o ⊢ op_exp_s8◂{ l sl dl } .
				» _^ dn ⊢ dn,sr
					0'*"\t" "» _^ .." " ⊢ .." "\n"
					" .. //\n" //
				∎ { dn sr }
		@.pnt_lc_mtc { dn m }
			∐ m ⊢ p_cns◂{ { l e } m } .
				pnt_lc_mtc { dn m } ⊢ { dn sm }
				$ e ⊢ 
				∐ mtc_rmf◂m0 .
					pnt_lc_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,sm,sm0 ⊢ dn,sm,sm0,sr
						0'*"\t" "∐ .. ;\n"
						2'
						1' //
					∎ { dn sr }
				∐ mtc_id_rmf◂{ sp m0 } . 
					pnt_src_ptn sp ⊢ ssp 
					pnt_lc_id_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,ssp,sm,sm0 ⊢ dn,ssp,sm,sm0,sr
						0'*"\t" "∐ .. ; " 1' "⊢ \n"
						3'
						2' //
					∎ { dn sr }
				∐. mtc_code◂lc .
					_inc dn ⊢ dn
					pnt_lc_code { dn lc } ⊢ { dn slc }
					_dec dn ⊢ dn
					» _^ dn,sm,slc ⊢ dn,sm,slc,sr
						0'*"\t" "∐ .. .\n"
						2'
						1' //
					∎ { dn sr }
			∐. m ⊢ p_end◂{ l e } .
			  ∐ e ⊢ mtc_rmf◂m0 .
					pnt_lc_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,sm0 ⊢ dn,sm0,sr
						0'*"\t" "∐. .. .\n"
						1' //
					∎ { dn sr }
				∐ e ⊢ mtc_id_rmf◂{ sp m0 } . 
					pnt_src_ptn sp ⊢ ssp 
					pnt_lc_id_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,ssp,sm0 ⊢ dn,ssp,sm0,sr
						0'*"\t" "∐ .. ; " 1' "⊢ \n"
						2'
						//
					∎ { dn sr }
				∐. e ⊢ mtc_code◂lc .
					_inc dn ⊢ dn
					pnt_lc_code { dn lc } ⊢ { dn slc }
					_dec dn ⊢ dn
					» _^ dn,slc ⊢ dn,slc,sr
						0'*"\t" "∐. .. .\n"
						1' //
					∎ { dn sr }
		@.pnt_lc_id_mtc { dn m }
			∐ m ⊢ p_cns◂{ { mp l e } m } .
				pnt_mtc_ptn mp ⊢ smp 
				pnt_lc_id_mtc { dn m } ⊢ { dn sm }
				$ e ⊢ 
				∐ mtc_rmf◂m0 .
					pnt_lc_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,smp,sm,sm0 ⊢ dn,smp,sm,sm0,sr
						0'*"\t" "∐ " 1' ".. ;\n"
						3'
						2' //
					∎ { dn sr }
				∐ mtc_id_rmf◂{ sp m0 } . 
					pnt_src_ptn sp ⊢ ssp 
					pnt_lc_id_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,ssp,sm,sm0 ⊢ dn,ssp,sm,sm0,sr
						0'*"\t" "∐ .. ; " 1' "⊢ \n"
						3'
						2' //
					∎ { dn sr }
				∐. mtc_code◂lc .
					_inc dn ⊢ dn
					pnt_lc_code { dn lc } ⊢ { dn slc }
					_dec dn ⊢ dn
					» _^ dn,sm,slc ⊢ dn,sm,slc,sr
						0'*"\t" "∐ .. .\n"
						2'
						1' //
					∎ { dn sr }
			∐. m ⊢ p_end◂{ mp l e } .
			  ∐ e ⊢ mtc_rmf◂m0 .
					pnt_mtc_ptn mp ⊢ smp 
					pnt_lc_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,sm0 ⊢ dn,sm0,sr
						0'*"\t" "∐. .. ;\n"
						1' //
					∎ { dn sr }
				∐ e ⊢ mtc_id_rmf◂{ sp m0 } .
					pnt_src_ptn sp ⊢ ssp 
					pnt_lc_id_mtc { dn m0 } ⊢ { dn sm0 }
					» _^ dn,ssp,sm0 ⊢ dn,ssp,sm0,sr
						0'*"\t" "∐ .. ; " 1' "⊢ \n"
						2' //
					∎ { dn sr }
				∐. e ⊢ mtc_code◂lc .
					_inc dn ⊢ dn
					pnt_lc_code { dn lc } ⊢ { dn slc }
					_dec dn ⊢ dn
					» _^ dn,slc ⊢ dn,slc,sr
						0'*"\t" "∐. .. .\n"
						1' //
					∎ { dn sr }
	§ @.pnt { dn f } 
		∐ f ⊢ _nil◂{} .
			» "" _ ⊢ s
			∎ { dn s }
		∐. f ⊢ _cns◂{ hd tl } .
			pnt_glb_etr { dn hd } ⊢ { dn s0 }
			pnt { dn tl } ⊢ { dn s1 }
			» _^ s0,s1 ⊢ s0,s1,sr
				0'
				1' //
			∎ { dn sr }
		@.pnt_glb_etr { dn e }
			∐ e ⊢ mdl_etr◂{ w me } .
				∐ me ⊢ mdl_def◂el .
					_inc dn ⊢ dn
					pnt { dn el } ⊢ { dn sl }
					_dec dn ⊢ dn
					» _^ dn,w,sl ⊢ dn,w,sl,sr
						0'*"\t" "§§ " 1' "\n"
							2'
						0'*"\t" "\t" "∎∎\n"
						//
					∎ { dn sr }
				∐. me ⊢ mdl_ln◂n .
					pnt_name n ⊢ sn
					» _^ dn,w,sn ⊢ dn,w,sn,sr
						0'*"\t" "§§ " 1' " = " 2' "\n"
						//
					∎ { dn sr }
			∐ e ⊢ grm_etr◂l .
				_inc dn ⊢ dn
				pnt_grm_etr { dn l } ⊢ { dn sl }
				_dec dn ⊢ dn
				» _^ dn,sl ⊢ dn,sl,sr
					0'*"\t" "§§+s8\n"
						1'
					//
				∎ { dn sr }
			∐ e ⊢ lc_etr◂{ w lce } .
				_inc dn ⊢ dn
				pnt_lc_etr { dn lce } ⊢ { dn se }
				_dec dn ⊢ dn
				» _^ dn,w,se ⊢ dn,w,se,sr
					0'*"\t" "§ " 1' 2'
					//
				∎ { dn sr }
			∐ e ⊢ lc_qlq_etr◂q .
				_inc dn ⊢ dn
				pnt_lc_qlq_etr { dn q } ⊢ { dn sq }
				_dec dn ⊢ dn
				» _^ dn,sq ⊢ dn,sq,sr
					0'*"\t" "§ \n"
						1'
					//
				∎ { dn sr }
			∐ e ⊢ dt_etr◂{ w d } .
				∐ d ⊢ dt_def◂l .
					_inc dn ⊢ dn
					pnt_dt_def { dn l } ⊢ { dn sl }
					_dec dn ⊢ dn
					» _^ dn,w,sl ⊢ dn,w,sl,sr
						0'*"\t" "¶ " 1' "\n"
							2' //
					∎ { dn sr }
				∐. d ⊢ dt_ln◂t .
					» _^ dn,w ⊢ dn,w,sr
						0'*"\t" "¶ " 1' " = .. \n" //
					∎ { dn sr }
			∐. e ⊢ dt_qlq_etr◂q .
				_inc dn ⊢ dn
				pnt_dt_qlq_etr { dn q } ⊢ { dn sq }
				_dec dn ⊢ dn
				» _^ dn,sq ⊢ dn,sq,sr
					0'*"\t" "¶\n"
						1'
					//
				∎ { dn sr }
		@.pnt_dt_qlq_etr { dn q }
			∐ q ⊢ _cns◂{ { w d } q } .
				_emt dn ⊢ dn
				pnt_dt_qlq_etr { dn q } ⊢ { dn sq }
				∐ d ⊢ dt_def◂l .
					_inc dn ⊢ dn
					pnt_dt_def { dn l } ⊢ { dn sl }
					_dec dn ⊢ dn
					» _^ dn,w,sl,sq ⊢ dn,w,sl,sq,sr
						0'*"\t" "@." 1' "\n"
							2'
						3' //
					∎ { dn sr }
				∐. d ⊢ dt_ln◂t .
					» _^ dn,w,sq ⊢ dn,w,sq,sr
						0'*"\t" "@." 1' " = .. \n"
						2' //
					∎ { dn sr }
			∐. q ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ { dn sr }
		@.pnt_grm_etr { dn q }
			∐ q ⊢ _cns◂{ { w g } q } .
				_inc dn ⊢ dn
				pnt_grm { dn g } ⊢ { dn sg }
				_dec dn ⊢ dn
				pnt_grm_etr { dn q } ⊢ { dn sq }
				» _^ dn,w,sg,sq ⊢ dn,w,sg,sq,sr
					0'*"\t" "@." 1' "\n" 2'
						3' //
				∎ { dn sr }
			∐. q ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ { dn sr }
		@.pnt_grm { dn g }
			∐ g ⊢ grm_cnc◂g .
				 pnt_grm_cnc { dn g } ⊢ { dn sr }
				 ∎ { dn sr }
			∐. g ⊢ grm_act◂g .
				pnt_grm_act { dn g } ⊢ { dn sr }
				∎ { dn sr }
		@.pnt_grm_cnc { dn g }
			∐ g ⊢ grm_cnc_seq◂{ cn o ps gc g } .
				pnt_grm_cnc { dn g } ⊢ { dn sg }
				» _^ dn,sg ⊢ dn,sg,sr
					0'*"\t" "∐ ..\n"
					1' //
				∎ { dn sr }
			∐. g ⊢ grm_cnc_end◂{ cn o ps gc } .
				» _^ dn ⊢ dn,sr
					0'*"\t" "∐. .. \n" //
				∎ { dn sr }
		@.pnt_grm_act { dn g }
			∐ g ⊢ grm_act_seq◂{ dp lc o ps gc g } .
				pnt_dst_ptn dp ⊢ sdp
				_inc dn ⊢ dn
				pnt_lc_code { dn lc } ⊢ { dn slc }
				_dec dn ⊢ dn
				pnt_grm_act { dn g } ⊢ { dn sg }
				» _^ dn,sdp,slc,sg ⊢ dn,sdp,slc,sg,sr
					0'*"\t" "∐ .. ⊢ " 1' "\n"
						2'
						3' //
				∎ { dn sr }
			∐. g ⊢ grm_act_end◂{ dp lc o ps gc } .
				pnt_dst_ptn dp ⊢ sdp
				_inc dn ⊢ dn
				pnt_lc_code { dn lc } ⊢ { dn slc }
				_dec dn ⊢ dn
				» _^ dn,sdp,slc ⊢ dn,sdp,slc,sr
					0'*"\t" "∐. .. ⊢ " 1' "\n"
						2' //
				∎ { dn sr }
		@.pnt_grm_rle { dn ps }
			∐ ps ⊢ grm_ptn_cut◂cps .
				pnt_grm_rle_cut cps ⊢ scps
				» _^ scps ⊢ scps,sr
					" !| " 0' //
				∎ { dn sr }
			∐ ps ⊢ grm_ptn_seq◂{ p ps } .
				pnt_grm_rle { dn ps } ⊢ { dn sps }
				» _^ sps ⊢ sps,sr
					" .. " 0' //
				∎ { dn sr }
			∐. ps ⊢ grm_ptn_end◂{} .
				» "" _ ⊢ sr
				∎ { dn sr }
		@.pnt_grm_rle_cut ps
			∐ ps ⊢ grm_rle_cut_seq◂{ p ps } .
				pnt_grm_rle_cut ps ⊢ sps
				» _^ sps ⊢ sps,sr
					" .. " 0' //
				∎ sr
			∐. ps ⊢ grm_rle_cut_end◂{} .
				» "" _ ⊢ sr
				∎ sr
		@.pnt_dt_etr { dn e }
			∐ e ⊢ dt_def◂l .
				_inc dn ⊢ dn
				pnt_dt_def { dn l } ⊢ { dn sl }
				_dec dn ⊢ dn
				» _^ sl ⊢ sl,sr
					"\n"
					0' //
				∎ { dn sl }
			∐. e ⊢ dt_ln◂t .
				pnt_type t ⊢ st
				» _^ st ⊢ st,sr
					" = " 0' "\n" //
				∎ { dn sr }
		@.pnt_dt_def { dn l }
			∐ l ⊢ _cns◂{ { w t } l } .
				pnt_dt_def { dn l } ⊢ { dn sl }
				pnt_type t ⊢ st
				» _^ dn,w,st,sl ⊢ dn,w,st,sl,sr
					0'*"\t" "∐ " 1' " : " 2' "\n"
					3' //
				∎ { dn sr }
			∐. l ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ { dn sr }
		@.pnt_type t
			∐ t ⊢ type_rcd◂l .
				pnt_type_rcd l ⊢ sl
				» _^ sl ⊢ sl,sr
					"{ " 0' "}" //
				∎ sr
			∐ t ⊢ imp◂{ t0 t1 } .
				pnt_type t0 ⊢ st0
				pnt_type t1 ⊢ st1
				» _^ st0,st1 ⊢ st0,st1,sr
					0' " → " 1' //
				∎ sr
			∐ t ⊢ app◂{ t0 t1 } .
				pnt_type t0 ⊢ st0
				pnt_type t1 ⊢ st1
				» _^ st0,st1 ⊢ st0,st1,sr
					0' "◂" 1' //
				∎ sr
			∐ t ⊢ type_name◂n .
				pnt_name n ⊢ sn
				∎ sn
			∐. t ⊢ type_var◂v .
				» _^ v ⊢ v,sr
					0' "'" //
				∎ sr
		@.pnt_type_rcd l
			∐ l ⊢ _cns◂{ t l } .
				pnt_type t ⊢ st
				pnt_type_rcd l ⊢ sl
				» _^ st,sl ⊢ st,sl,sr
					0' " " 1' //
				∎ sr
			∐. l ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ sr
		@.pnt_lc_etr { dn e }
			∐ e ⊢ lc_def◂{ dp lc } .
				pnt_dst_ptn dp ⊢ sdp
				_inc dn ⊢ dn
				pnt_lc_code { dn lc } ⊢ { dn slc }
				_dec dn ⊢ dn
				» _^ sdp,slc ⊢ sdp,slc,sr
					" " 0' "\n"
					1' //
				∎ { dn sr }
			∐ e ⊢ lc_ln◂c .
				pnt_cst c ⊢ sc
				» _^ sc ⊢ sc,sr
					" = " 0' "\n" //
				∎ { dn sr }
			∐. e ⊢ lc_cst◂lc .
				_inc dn ⊢ dn
				pnt_lc_code { dn lc } ⊢ { dn slc }
				_dec dn ⊢ dn
				» _^ slc ⊢ slc,sr
					" « \n"
					0'
					//
				∎ { dn sr }
		@.pnt_lc_qlq_etr { dn q }
			∐ q ⊢ _cns◂{ { w dp lc } q } .  
				pnt_dst_ptn dp ⊢ sdp
				pnt_lc_qlq_etr { dn q } ⊢ { dn sq }
				_inc dn ⊢ dn
				pnt_lc_code { dn lc } ⊢ { dn slc }
				_dec dn ⊢ dn
				» _^ dn,w,sdp,slc,sq ⊢ dn,w,sdp,slc,sq,sr
					0'*"\t" "@." 1' " " 2' "\n"
						3'
					4'
					//
				∎ { dn sr } 
			∐. q ⊢ _nil◂{} .
				» "" _ ⊢ sr
				∎ { dn sr }
				
	∎∎
	§§ syntax
	§§+s8 
		@.wc 
			∐. "_" !| ; ⊢ { {} },p 
				_some {} ⊢ a 
				∎ a
				∐ " " ⊢ { {} } 
					_some {} ⊢ a 
					∎ a
				∐ "\t" ⊢ { {} } 
					_some {} ⊢ a 
					∎ a 
				∐ wc_tl !| ⊢ { xx }
					_none {} ⊢ a 
					∎ a
				∐. ⊢ { } 
					_some {} ⊢ a 
					∎ a 
		@.wc_tl 
			∐ "," !| ⊢ { {} }
				_none {} ⊢ a 
				∎ a 
			∐. ⊢ { }
				_some {} ⊢ a  
				∎ a
	§§+s8
		@.name
			∐# prm_word "." name ⊢ { w {} n }
				ast.mdl_n { w n } ⊢ r
				_some r ⊢ a
				∎ a
			∐.# prm_word ⊢ { w }
				ast.fst_n w ⊢ r
				_some r ⊢ a
				∎ a
	§§+s8
		@.cst
			∐ _scf_d "r" ⊢ { x {} }
				ast.r64 x ⊢ r
				ast.cst_v r ⊢ r 
				_some r ⊢ a
				∎ a
			∐ stg_ltr ⊢ { s }
				ast.s8 s ⊢ r
				ast.cst_v r ⊢ r 
				_some r ⊢ a
				∎ a
			∐. name ⊢ { n }
				ast.cst_name n ⊢ r
				_some r ⊢ a
				∎ a
	§§+s8
		@.nl
			∐.# "\n" ⟦"\n"⟧
	¶ prs_rslt◂a'
		∐ prs_vld : a'
		∐ prs_err : _lst◂{ _r64 _r64 _s8 } 
	
	§ @.pnt_prs_err el 
		$ el ⊢ 
		∐ _cns◂{ { n0 n1 em } el } . 
			pnt_prs_err el ⊢ sel 
			» _^ n0,n1,em,sel ⊢ n0,n1,em,sel,sr 
				"prs_err " 0d' "-" 1d' ":" 2' "\n" 
				3' // 
			∎ sr   
		∐. _nil◂{} . 
			» "" _ ⊢ sr 
			∎ sr 
			
	§ @.mk_app { a l }
		∐ l ⊢ _cns◂{ hd tl } .
			ast.app { a hd } ⊢ a
			mk_app { a tl } ⊢ a
			∎ a
		∐. l ⊢ _nil◂{} .
			∎ a

	§§+s8
		@.mtc_ptn
			∐* "{" mtc_ptn_lst "}" ⊢ { {} l {} }
				ast.RP.rcd l ⊢ r
				_some r ⊢ a
				∎ a
			∐.* mtc ⊢ { s }
				ast.RP.atm s ⊢ r
				_some r ⊢ a
				∎ a
		@.mtc_ptn_lst
			∐* mtc_ptn mtc_ptn_lst ⊢ { hd tl }
				_cns { hd tl } ⊢ l
				_some l ⊢ a
				∎ a
			∐.* ⊢ {}
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.mtc_tl
			∐* "," !| mtc mtc_tl ⊢ { {} r rs }
				##ast.RP.atm r ⊢ r
				_cns { r rs } ⊢ rs
				_some rs ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ rs
				_some rs ⊢ a
				∎ a
		@.mtc
			∐* name "◂" mtc_ptn ⊢ { n {} mp } 
				ast.mtc_agl { n mp } ⊢ p 
				_some p ⊢ a 
				∎ a
			∐* "%[" !| cst "]" ⊢ { {} c {} }
				ast.mtc_cst c ⊢ r
				_some r ⊢ a
				∎ a
			∐* "%" !| prm_word ⊢ { {} w }
				ast.mtc_reg w ⊢ r
				_some r ⊢ a
				∎ a
			∐* prm_word ⊢ { w }
				ast.mtc_reg w ⊢ r
				_some r ⊢ a
				∎ a
			∐.* "_" ⊢ { {} }
				ast.mtc_wc {} ⊢ r 
				_some r ⊢ a 
				∎ a
	§§+s8
		@.src_ptn
			∐* "{" src_ptn_lst "}" ⊢ { {} l {} }
				ast.RP.rcd l ⊢ r
				_some r ⊢ a
				∎ a
			∐.* src ⊢ { s }
				ast.RP.atm s ⊢ r
				_some r ⊢ a
				∎ a
		@.src_ptn_lst
			∐* src_ptn src_ptn_lst ⊢ { hd tl }
				_cns { hd tl } ⊢ l
				_some l ⊢ a
				∎ a
			∐.* ⊢ {}
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.src_tl
			∐* "," !| src src_tl ⊢ { {} r rs }
				##ast.RP.atm r ⊢ r
				_cns { r rs } ⊢ rs
				_some rs ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ rs
				_some rs ⊢ a
				∎ a
		@.src
			∐* "%[" cst "]" ⊢ { {} c {} }
				ast.im_reg c ⊢ r
				_some r ⊢ a
				∎ a
			∐ "%" !| prm_word ⊢ { {} w }
				ast.src_reg w ⊢ r
				_some r ⊢ a
				∎ a
			∐.* prm_word ⊢ { w }
				ast.src_reg w ⊢ r
				_some r ⊢ a
				∎ a
	§§+s8
		@.dst_ptn
			∐* "{" dst_ptn_lst "}" ⊢ { {} l {} }
				ast.RP.rcd l ⊢ r
				_some r ⊢ a
				∎ a
			∐.* dst ⊢ { s }
				ast.RP.atm s ⊢ r
				_some r ⊢ a
				∎ a
		@.dst_ptn_lst
			∐* dst_ptn dst_ptn_lst ⊢ { hd tl }
				_cns { hd tl } ⊢ l
				_some l ⊢ a
				∎ a
			∐.* ⊢ {}
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.dst_ptn_tl
			∐* "," !| dst_ptn dst_ptn_tl ⊢ { {} d ds }
				_cns { d ds } ⊢ ds
				_some ds ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ ds
				_some ds ⊢ a
				∎ a
		@.dst_tl
			∐* "," !| dst dst_tl ⊢ { {} r rs }
				##ast.RP.atm r ⊢ r
				_cns { r rs } ⊢ rs
				_some rs ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ rs
				_some rs ⊢ a
				∎ a
		@.dst
			∐ "%" !| word ⊢ { {} w }
				ast.dst_reg w ⊢ r
				_some r ⊢ a
				∎ a
			∐ word ⊢ { w }
				ast.dst_reg w ⊢ r
				_some r ⊢ a
				∎ a
			∐. "_" ⊢ { {} }
				ast.wc {} ⊢ r
				_some r ⊢ a
				∎ a
	§§+s8
		@.type
			∐.* type_atm type_app_tl type_tl ⊢ { t0 t1 t2 }
				mk_app { t0 t1 } ⊢ t
				∐ t2 ⊢ _some◂t2 .
					ast.imp { t t2 } ⊢ t
					_some t ⊢ a
					∎ a
				∐. t2 ⊢ _none◂{} .
					_some t ⊢ a
					∎ a
		@.type_tl
			∐* "→" type ⊢ { {} t }
				_some t ⊢ a
				_some a ⊢ a
				∎ a
			∐.* ⊢ { }
				_none {} ⊢ r
				_some r ⊢ a
				∎ a
		@.type_app_tl
			∐* "◂" type_atm type_app_tl ⊢ { {} t0 t1 }
				_cns { t0 t1 } ⊢ r
				_some r ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ r
				_some r ⊢ a
				∎ a
		@.type_atm
			∐* "{" ⟦type⟧ "}" ⊢ { {} l {} }
				rev l ⊢ l
				ast.type_rcd l ⊢ t
				_some t ⊢ a
				∎ a
			∐* "(" type ")" ⊢ { {} t {} }
				_some t ⊢ a
				∎ a
			∐* var ⊢ { v }
				ast.type_var v ⊢ r
				_some r ⊢ a
				∎ a
			∐.* name ⊢ { n }
				ast.type_name n ⊢ r
				_some r ⊢ a
				∎ a
		@.var
			∐. word "'" ⊢ { w {} }
				_some w ⊢ a
				∎ a
			
	§§+s8 
		@.nls_p 
			∐# "\n" !| nls 
			∐# _byt !| "\\F\\" 
			∐.# 
		@.nls 
			∐# "\n" !| nls ⊢ { {} xx }
				prs_vld {} ⊢ p 
				_some p ⊢ a 
				∎ a 
			∐# "##" !| line_res nls ⊢ { {} xx yy } 
				prs_vld {} ⊢ p 
				_some p ⊢ a 
				∎ a
			∐# "\\[" !| ; ⊢ { {} },p
				_some p ⊢ a 
				∎ a
				∐# "\n" cmt_in nls ⊢ { {} p0 p1 }
					$ p0 ⊢ 
					∐ prs_vld◂p0 . 
						$ p1 ⊢ 
						∐ prs_vld◂p1 . 
							prs_vld {} ⊢ p 
							_some p ⊢ a 
							∎ a 
						∐. prs_err◂e1 . 
							pnt_prs_err e1 ⊢ s 
							_emt s ⊢ s 
							∎|
					∐. prs_err◂e0 . 
						$ p1 ⊢ 
						∐ prs_vld◂p1 . 
							pnt_prs_err e0 ⊢ s 
							_emt s ⊢ s
							∎| 
						∐. prs_err◂e1 . 
							apnd { e0 e1 } ⊢ e 
							pnt_prs_err e ⊢ s 
							_emt s ⊢ s
							∎|
				∐.# ⊢ { } 
					##» _^ n,r ⊢ n,r,sr 
					» _^ _n,_r ⊢ n,r,sr 
						"nls_err " 0d' "-" 1d' ":\n" //
					_emt sr ⊢ sr 
					∎|   
			∐.# ⊢ { } 
				prs_vld {} ⊢ p 
				_some p ⊢ a 
				∎ a
		@.cmt_in 
			∐# "\\[" !| ; ⊢ { {} },p
				_some p ⊢ a 
				∎ a 
				∐# "\n" cmt_in cmt_in ⊢ { {} p0 p1 }
					$ p0 ⊢ 
					∐ prs_vld◂p0 . 
						$ p1 ⊢ 
						∐ prs_vld◂p1 . 
							prs_vld {} ⊢ p 
							_some p ⊢ a 
							∎ a 
						∐. prs_err◂e1 . 
							prs_err e1 ⊢ p 
							_some p ⊢ a 
							∎ a
					∐. prs_err◂e0 . 
						$ p1 ⊢ 
						∐ prs_vld◂p1 . 
							prs_err e0 ⊢ p 
							_some p ⊢ a 
							∎ a 
						∐. prs_err◂e1 . 
							apnd { e0 e1 } ⊢ e 
							prs_err e ⊢ p 
							_some p ⊢ a 
							∎ a
					∐.# ⊢ { } 
						_nil {} ⊢ l 
						» "blk_cmt 0" _ ⊢ s
						_cns { { _n _r s } l } ⊢ l 
						prs_err l ⊢ l 
						_some l ⊢ a 
						∎ a 
			∐# "\\]" !| "\n" ⊢ { {} {} }
				prs_vld {} ⊢ p 
				_some p ⊢ a 
				∎ a  
			∐# line_res cmt_in ⊢ { p0 p1 } 
				_some p1 ⊢ a 
				∎ a
			∐.# ⊢ { } 
				_nil {} ⊢ l 
				» "blk_cmt 1" _ ⊢ s
				_cns { { _n _r s } l } ⊢ l 
				prs_err l ⊢ l 
				_some l ⊢ a 
				∎ a
		@.line_res 
			∐# "\n" ⊢ { {} }
				_some {} ⊢ a 
				∎ a 
			∐# _byt line_res ⊢ { b l }
				_some {} ⊢ a
				∎ a  
			∐.# ⊢ { }
				_some {} ⊢ a 
				∎ a
	§§+s8 
		@.lc_code
			∐* nls "∎|" ⊢ { nn {} }
				ast.exn {} ⊢ c
				_some c ⊢ a
				∎ a
			∐* nls "∎" !| src_ptn ⊢ { nn {} r }
				ast.ret r ⊢ c
				_some c ⊢ a
				∎ a
			∐* lc_mtc ⊢ { m }
				ast.mtc m ⊢ m
				_some m ⊢ a
				∎ a
			∐* nls "$" !| src_ptn "⊢" id_line ⊢ { nn {} s {} il }
				ast.op_id { s il } ⊢ l 
				_some l ⊢ a 
				∎ a
			∐* nls "»" !| lc_exp_line lc_code ⊢ { nn {} l lc } 
				ast.seq { l lc } ⊢ lc 
				_some lc ⊢ a
				∎ a
			∐* nls "◂" !| src "," src_ptn ; ⊢ { nn {} r0 {} r1},o
				$ o ⊢ 
				∐ _some◂{ d lc } . 
					ast.op_app { r0 r1 d } ⊢ e
					ast.seq { e lc } ⊢ c
					_some c ⊢ a 
					∎ a 
				∐. _none◂{} . 
					ast.app_jmp { r0 r1 } ⊢ c 
					_some c ⊢ a 
					∎ a  
				∐* "⊢|" !| ⊢ { {} } 
					_none {} ⊢ o 
					_some o ⊢ a 
					∎ a  
				∐.* "⊢" dst_ptn lc_code ⊢ { {} r2 lc }
					_some { r2 lc } ⊢ a 
					_some a ⊢ a 
					∎ a 
			∐.* nls name src_ptn ; ⊢ { nn n r0 },o 
				$ o ⊢ 
				∐ _some◂{ y lc } .
					ast.op_name { n r0 y } ⊢ e
					ast.seq { e lc } ⊢ e 
					_some e ⊢ a
					∎ a	
				∐. _none◂{} . 
					ast.jmp { n r0 } ⊢ lc 
					_some lc ⊢ a 
					∎ a 
				∐* "⊢|" !| ⊢ { {} }
					_none {} ⊢ o 
					_some o ⊢ a 
					∎ a  
				∐.* "⊢" dst_ptn lc_code ⊢ { {} r1 lc }
					_some { r1 lc } ⊢ o
					_some o ⊢ a 
					∎ a 
		@.id_line
			∐* lc_id_mtc ⊢ { m }
				ast.id_mtc m ⊢ m 
				_some m ⊢ a 
				∎ a 
			∐.* dst_ptn dst_ptn_tl lc_code ⊢ { r1 rs lc }
				_cns { r1 rs } ⊢ rs
				ast.id_line { rs lc } ⊢ e
				_some e ⊢ a
				∎ a
		@.lc_exp_line
			∐* "_^" !| lc_exp_s8_sd ⟦s8_ptn⟧ "//" ⊢ { {} { s d } ps {} }
				ast.op_exp_s8 { ps s d } ⊢ e
				_some e ⊢ a
				∎ a
			∐.* cst "_" "⊢" dst_ptn ⊢ { c {} {} r }
				_nil {} ⊢ l
				ast.RP.rcd l ⊢ s
				ast.op_exp_cst { c s r } ⊢ e
				_some e ⊢ a
				∎ a
		@.lc_exp_s8_sd
			∐* wc !| "⊢" dst ⊢ { xx {} r }
				_nil {} ⊢ l0
				_nil {} ⊢ l1
				_cns { r l1 } ⊢ l1
				_some { l0 l1 } ⊢ a
				∎ a
			∐.* src src_tl "⊢" dst dst_tl ⊢ { r0 rs0 {} r1 rs1 }
				_cns { r0 rs0 } ⊢ rs0
				_cns { r1 rs1 } ⊢ rs1
				_some { rs0 rs1 } ⊢ a
				∎ a
		@.s8_ptn
			∐* var_idx "*" !| stg_ltr ⊢ { i {} s }
				ast.exp_s8_for { i s } ⊢ r
				_some r ⊢ a
				∎ a
			∐* s8_ptn_line ⊢ { s }
				ast.exp_s8_s8 s ⊢ r
				_some r ⊢ a
				∎ a
			∐* stg_ltr ⊢ { s }
				ast.exp_s8_s8 s ⊢ s
				_some s ⊢ a
				∎ a
			∐* name ⊢ { n }
				ast.cst_name n ⊢ c 
				ast.exp_s8_cst c ⊢ n
				_some n ⊢ a
				∎ a
			∐.* s8_fmt ⊢ { f }
				ast.exp_s8_idx f ⊢ r
				_some r ⊢ a
				∎ a
		@.var_idx
			∐. _scf_d "'" ⊢ { x {} }
				_some x ⊢ a
				∎ a
		@.s8_fmt
			∐ _scf_d "d'" ⊢ { d {} }
				ast.exp_fmt_d {} ⊢ f
				_some { f d } ⊢ a
				∎ a
			∐ _scf_d "x'" ⊢ { d {} }
				ast.exp_fmt_x {} ⊢ f
				_some { f d } ⊢ a
				∎ a
			∐. _scf_d "'" ⊢ { d {} }
				ast.exp_fmt_s {} ⊢ f
				_some { f d } ⊢ a
				∎ a
		@.s8_ptn_line
			∐. "`" line ⊢ { {} l }
				s8_of_r64_lst l ⊢ s
				_some s ⊢ a
				∎ a
		@.line
			∐ "\n" ⊢ { {} }
				» 10r _ ⊢ s
				_nil {} ⊢ l
				_cns { s l } ⊢ l
				_some l ⊢ a
				∎ a
			∐. _byt line ⊢ { b l }
				_cns { b l } ⊢ l
				_some l ⊢ a
				∎ a
		@.lc_mtc
			∐* nls "∐." !| mtc_line lc_code ⊢ { nn {} l c0 }
				ast.mtc_code c0 ⊢ c1
				p_end { l c1 } ⊢ m
				_some m ⊢ a
				∎ a
			∐.* nls "∐" mtc_line lc_code lc_mtc ⊢ { nn {} l c m }
				ast.mtc_code c ⊢ c
				p_cns { { l c } m } ⊢ m
				_some m ⊢ a
				∎ a
		@.lc_id_mtc
			∐* nls "∐." !| mtc_ptn mtc_line lc_code ⊢ { nn {} mp l c }
				ast.mtc_code c ⊢ c
				p_end { mp l c } ⊢ m
				_some m ⊢ a
				∎ a
			∐.* nls "∐" mtc_ptn mtc_line lc_code lc_id_mtc ⊢ { nn {} mp l c m }
				ast.mtc_code c ⊢ c
				p_cns { { mp l c } m } ⊢ m
				_some m ⊢ a
				∎ a
		@.mtc_line
			∐# mtc_test mtc_test_tl ⊢ { e0 l }
				_cns { e0 l } ⊢ l
				_some l ⊢ a
				∎ a
			∐.# "." "\n" ⊢ { {} {} }
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.mtc_test
			∐# src_ptn "⊢" !| name "◂" mtc_ptn ⊢ { r0 {} n {} r1 }
				ast.mtc_agl { n r1 } ⊢ mp 
				ast.RP.atm mp ⊢ mp 
				ast.mtc_id { r0 mp } ⊢ r
				_some r ⊢ a
				∎ a
			∐.# mtc_val "=" mtc_val ⊢ { v0 {} v1 }
				ast.mtc_test { v0 v1 } ⊢ r
				_some r ⊢ a
				∎ a
		@.mtc_test_tl
			∐# ";" !| mtc_test mtc_test_tl ⊢ { {} e l }
				_cns { e l } ⊢ l
				_some l ⊢ a
				∎ a
			∐.# "." "\n" ⊢ { {} {} }
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.mtc_val
			∐ word "'" ⊢ { w {} }
				ast.test_var w ⊢ v
				_some v ⊢ a
				∎ a
			∐. cst ⊢ { c }
				ast.test_cst c ⊢ v
				_some v ⊢ a
				∎ a
	§§+s8
		@.file
			∐* nls glb_etr file ⊢ { nn e gl }
				$ gl ⊢ 
				∐ prs_vld◂gl . 
					$ e ⊢ 
					∐ prs_vld◂e . 
						_cns { e gl } ⊢ gl
						prs_vld gl ⊢ gl 
						_some gl ⊢ a
						∎ a
					∐. prs_err◂err . 
						prs_err err ⊢ e 
						_some e ⊢ a 
						∎ a 
				∐. prs_err◂err .  
					$ e ⊢ 
					∐ prs_vld◂e . 
						prs_err err ⊢ err 
						_some err ⊢ a
						∎ a
					∐. prs_err◂err0 . 
						apnd { err0 err } ⊢ err 
						prs_err err ⊢ e 
						_some e ⊢ a 
						∎ a   
			∐.* nls ; ⊢ { nn },p 
				_nil {} ⊢ gl
				prs_vld gl ⊢ gl  
				_some gl ⊢ a
				∎ a
				∐* _byt !| ⊢ { b }
					» "!!!" _ ⊢ xx 
					_emt xx ⊢ xx 
					_none {} ⊢ a 
					∎ a   
				∐.* ⊢ { } 
					» "???" _ ⊢ xx 
					_emt xx ⊢ xx 
					_some {} ⊢ a 
					∎ a 
		@.glb_etr
			∐* "§§+s8" !| ; ⊢ { {} },p 
				_some p ⊢ a
				∎ a 
				∐* grm_etr ⟦grm_etr⟧ ⊢ { e l }
					rev l ⊢ l
					_cns { e l } ⊢ l
					ast.grm_etr l ⊢ l
					prs_vld l ⊢ l 
					_some l ⊢ a
					∎ a
				∐.* glb_etr_res ⊢ { bs }
					_nil {} ⊢ l 
					» "grb_etr 1" _ ⊢ s 
					_cns { { _n _r s } l } ⊢ l 
					prs_err l ⊢ e 
					_some e ⊢ a
					∎ a
			∐* nls "§§" !| ; ⊢ { nn {} },p 
				_some p ⊢ a 
				∎ a
				∐* word mdl_etr_def ⊢ { n d }
					$ d ⊢ 
					∐ prs_vld◂d . 
						ast.mdl_etr { n d } ⊢ r
						prs_vld r ⊢ r 
						_some r ⊢ a
						∎ a 
					∐. prs_err◂err . 
						prs_err err ⊢ err 
						_some err ⊢ a 
						∎ a 
				∐.* mdl_etr_res ⊢ { r } 
					_nil {} ⊢ l 
					» "glb_etr 0.1" _ ⊢ s
					_cns { { _n _r s } l } ⊢ l
					prs_err l ⊢ e 
					_some e ⊢ a 
					∎ a
			∐* "§" !| ; ⊢ { {} },p 
				_some p ⊢ a 
				∎ a
				∐* lc_etr_body ⊢ { le }
					prs_vld le ⊢ le 
					_some le ⊢ a
					∎ a
				∐.* glb_etr_res ⊢ { bs }
					_nil {} ⊢ l 
					» "glb_etr 3" _ ⊢ s 
					_cns { { _n _r s } l } ⊢ l 
					prs_err l ⊢ e 
					_some e ⊢ a
					∎ a
			∐.* "¶" !| ; ⊢ { {} },p 
				_some p ⊢ a
				∎ a 
				∐* dt_etr ⊢ { de }
					prs_vld de ⊢ de 
					_some de ⊢ a
					∎ a
				∐.* glb_etr_res ⊢ { bs }
					_nil {} ⊢ l 
					» "glb_etr 4" _ ⊢ s 
					_cns { { _n _r s } l } ⊢ l 
					prs_err l ⊢ e 
					_some e ⊢ a
					∎ a
		@.glb_etr_res 
			∐* glb_etr_res_ch glb_etr_res ⊢ { c r }
				_some {} ⊢ a 
				∎ a
			∐.* ⊢ { }
				_some {} ⊢ a 
				∎ a
		@.mdl_etr_res 
			∐* "∎∎" ⊢ { {} } 
				_some {} ⊢ a
				∎ a 
			∐* "§§" ; ⊢ { {} },p
				_some {} ⊢ a 
				∎ a 
				∐* word "=" name mdl_etr_res ⊢ { w {} n r }
					_some {} ⊢ a 
					∎ a 
				∐.* mdl_etr_res mdl_etr_res ⊢ { r0 r1 } 
					_some {} ⊢ a 
					∎ a 
			∐* _byt mdl_etr_res ⊢ { b r } 
				_some {} ⊢ a 
				∎ a
			∐.* ⊢ { } 
				_some {} ⊢ a   
				∎ a 
		@.glb_etr_res_ch 
			∐* "§§+s8" !| "\\F\\" ⊢ { {} {} }
				∎|
			∐* "§§" !| "\\F\\" ⊢ { {} {} }
				∎| 
			∐* "§" !| "\\F\\" ⊢ { {} {} }
				∎|  
			∐* "¶" !| "\\F\\" ⊢ { {} {} }
				∎| 
			∐* "∎∎" !| "\\F\\" ⊢ { {} {} } 
				∎| 
			∐.* _byt ⊢ { b } 
				_some {} ⊢ a 
				∎ a 
		@.mdl_etr_def
			∐* "=" !| name nls ⊢ { {} n nn }
				ast.mdl_ln n ⊢ r
				prs_vld r ⊢ r 
				_some r ⊢ a
				∎ a
			∐.* nls mdl_glb_etr ⊢ { nn m }
				$ m ⊢ 
				∐ prs_vld◂m . 
					ast.mdl_def m ⊢ r
					prs_vld r ⊢ r 
					_some r ⊢ a
					∎ a
				∐. prs_err◂err . 
					prs_err err ⊢ err 
					_some err ⊢ a 
					∎ a
		@.mdl_glb_etr
			∐* nls "∎∎" nls ⊢ { nn0 {} nn }
				_nil {} ⊢ l
				prs_vld l ⊢ l 
				_some l ⊢ a
				∎ a
			∐* nls glb_etr mdl_glb_etr ⊢ { nn e gl }
				$ gl ⊢ 
				∐ prs_vld◂gl . 
					$ e ⊢ 
					∐ prs_vld◂e . 
						_cns { e gl } ⊢ gl
						prs_vld gl ⊢ gl 
						_some gl ⊢ a
						∎ a
					∐. prs_err◂err . 
						prs_err err ⊢ e 
						_some e ⊢ a 
						∎ a 
				∐. prs_err◂err .  
					$ e ⊢ 
					∐ prs_vld◂e . 
						prs_err err ⊢ err 
						_some err ⊢ a
						∎ a
					∐. prs_err◂err0 . 
						apnd { err0 err } ⊢ err 
						prs_err err ⊢ e 
						_some e ⊢ a 
						∎ a  
			∐. mdl_etr_res ⊢ { r }
				_nil {} ⊢ l 
				» "glb_etr 4" _ ⊢ s 
				_cns { { _n _r s } l } ⊢ l 
				prs_err l ⊢ e 
				_some e ⊢ a
				∎ a
		@.dt_etr
			∐* "@" !| "." dt_etr_body qlq_dt_etr ⊢ { {} {} e q }
				_cns { e q } ⊢ q
				ast.dt_qlq_etr q ⊢ r
				_some r ⊢ a
				∎ a
			∐* dt_etr_body ⊢ { e }
				ast.dt_etr e ⊢ r
				_some r ⊢ a
				∎ a
			∐. ⊢ { }
				» "dt_etr 0" _ ⊢ s
				_emt s ⊢ s
				_none {} ⊢ a
				∎ a
		@.dt_etr_body
			∐* word dt_args "=" !| type ⊢ { w as {} n }
				ast.dt_ln n ⊢ r
				_some { w r } ⊢ a
				∎ a
			∐.* word dt_args nls "∐" word ":" type dt_etr_coprd ⊢ { w as nn {} wc {} t d }
				_cns { { wc t } d } ⊢ c
				ast.dt_def c ⊢ r
				_some { w r } ⊢ a
				∎ a
		@.dt_args
			∐* "◂" !| var args_tl 
			∐* var !| args_tl 
			∐.* 
		@.args_tl 
			∐* "," !| var args_tl 
			∐.* 
		@.qlq_dt_etr
			∐* nls "@" !| "." dt_etr_body qlq_dt_etr ⊢ { nn {} {} p q }
				_cns { p q } ⊢ l
				_some l ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.dt_etr_coprd
			∐* nls "∐" !| word ":" type dt_etr_coprd ⊢ { nn {} w {} t d }
				_cns { { w t } d } ⊢ l
				_some l ⊢ a
				∎ a
			∐.* ⊢ { }
				_nil {} ⊢ l
				_some l ⊢ a
				∎ a
		@.lc_etr_body
			∐* nls "@" !| "." word dst_ptn lc_code ⟦qlq_etr⟧ ⊢ { nn {} {} w dp lc q }
				rev q ⊢ q
				_cns { { w dp lc } q } ⊢ r
				ast.lc_qlq_etr r ⊢ r
				_some r ⊢ a
				∎ a
			∐* word lc_etr_def ⊢ { n e }
				ast.lc_etr { n e } ⊢ r
				_some r ⊢ a
				∎ a
			∐. ⊢ { }
				» "lc_etr_body 0" _ ⊢ s
				_emt s ⊢ s
				_none {} ⊢ a
				∎ a
		@.qlq_etr
			∐.* nls "@" "." word dst_ptn lc_code ⊢ { nn {} {} w dp lc }
				_some { w dp lc } ⊢ a
				∎ a
		@.lc_etr_def
			∐* "=" !| cst ⊢ { {} c }
				ast.lc_ln c ⊢ r
				_some r ⊢ a
				∎ a
			∐* "«" !| lc_code ⊢ { {} x }
				ast.lc_cst x ⊢ r
				_some r ⊢ a
				∎ a
			∐.* dst_ptn lc_code ⊢ { r0 x0 }
				ast.lc_def { r0 x0 } ⊢ r
				_some r ⊢ a
				∎ a
		
		@.grm_etr
			∐.* "@" "." word grm_etr_def ⊢ { {} {} w d }
				_some { w d } ⊢ a
				∎ a
		@.grm_etr_def
			∐* grm_etr_act ⊢ { g }
				ast.grm_act g ⊢ g
				_some g ⊢ a
				∎ a
			∐.* grm_etr_cnc ⊢ { g }
				ast.grm_cnc g ⊢ g
				_some g ⊢ a
				∎ a
		@.grm_etr_act
			∐* grm_ord_end !| grm_rle grm_etr_act_act	⊢ { o l { r0 lc a } }
				ast.grm_act_end { r0 lc o l a } ⊢ e
				_some e ⊢ a
				∎ a
			∐.* grm_ord grm_rle grm_etr_act_act grm_etr_act ⊢ { o l { r0 lc a } g }
				ast.grm_act_seq { r0 lc o l a g } ⊢ e
				_some e ⊢ a
				∎ a
		@.grm_etr_act_act
			∐* ";" "⊢" !| dst_ptn "," dst_ptn lc_code grm_etr_act ⊢ { {} {} r0 {} r1 lc g }
				_some { r1 g } ⊢ r
				_some { r0 lc r } ⊢ a
				∎ a
			∐.* "⊢" !| dst_ptn lc_code ⊢ { {} r0 lc }
				_none {} ⊢ r
				_some { r0 lc r } ⊢ a
				∎ a
		@.grm_rle
			∐* "!|" !| grm_rle_cut ⊢ { {} g }
				ast.grm_ptn_cut g ⊢ g
				_some g ⊢ r
				∎ r
			∐* grm_ptn grm_rle ⊢ { h g }
				ast.grm_ptn_seq { h g } ⊢ r
				_some r ⊢ a
				∎ a
			∐.* ⊢ { }
				ast.grm_ptn_end {} ⊢ r
				_some r ⊢ a
				∎ a
		@.grm_rle_cut
			∐* grm_ptn grm_rle_cut ⊢ { p g }
				ast.grm_rle_cut_seq { p g } ⊢ r
				_some r ⊢ a
				∎ a
			∐.* ⊢ { }
				ast.grm_rle_cut_end {} ⊢ r
				_some r ⊢ a
				∎ a
		@.grm_ptn
			∐* "⟦" grm_ptn_atm "⟧" ⊢ { {} p {} }
				ast.grm_lst p ⊢ r
				_some r ⊢ a
				∎ a
			∐.* grm_ptn_atm ⊢ { a }
				ast.grm_atm a ⊢ a
				_some a ⊢ a
				∎ a
		@.grm_ptn_atm
			∐* stg_ltr ⊢ { a }
				ast.grm_s8 a ⊢ a
				_some a ⊢ a
				∎ a
			∐.* name ⊢ { n }
				ast.grm_name n ⊢ a
				_some a ⊢ a
				∎ a
		@.grm_ord
			∐* "∐*" ⊢ { {} }
				ast.synt {} ⊢ r
				_some r ⊢ a
				∎ a
			∐* "∐#" ⊢ { {} }
				ast.line {} ⊢ r
				_some r ⊢ a
				∎ a
			∐.* "∐" ⊢ { {} }
				ast.lex {} ⊢ r
				_some r ⊢ a
				∎ a
		@.grm_ord_end
			∐* "∐.*" ⊢ { {} }
				ast.synt {} ⊢ r
				_some r ⊢ a
				∎ a
			∐* "∐.#" ⊢ { {} }
				ast.line {} ⊢ r
				_some r ⊢ a
				∎ a
			∐.* "∐." ⊢ { {} }
				ast.lex {} ⊢ r
				_some r ⊢ a
				∎ a
		@.grm_etr_cnc
			∐* grm_ord_end !| cnc_cn grm_rle grm_etr_cnc_seq ⊢ { o cn ps g }
				ast.grm_cnc_end { cn o ps g } ⊢ r
				_some r ⊢ a
				∎ a
			∐.* grm_ord cnc_cn grm_rle grm_etr_cnc_seq grm_etr_cnc ⊢ { o cn ps g0 g1 }
				ast.grm_cnc_seq { cn o ps g0 g1 } ⊢ r
				_some r ⊢ a
				∎ a
		@.grm_etr_cnc_seq
			∐* ";" grm_etr_cnc ⊢ { {} g }
				_some g ⊢ a
				_some a ⊢ a
				∎ a
			∐.* ⊢ { }
				_none {} ⊢ r
				_some r ⊢ a
				∎ a
		@.cnc_cn
			∐* word ":" ⊢ { w {} }
				_some w ⊢ r
				_some r ⊢ a
				∎ a
			∐.* ⊢ { }
				_none {} ⊢ r
				_some r ⊢ a
				∎ a		
	∎∎
	§§ ns
		¶ @.reg_env = _lst◂{ _s8 _opn◂_r64 }
			@.mdl_env = { _lst◂{ _s8 _opn◂_r64 } type_env etr_env reg_env }
			@.etr_env = _lst◂{ _s8 _opn◂_r64 }
			@.type_env = _lst◂{ _s8 _opn◂_r64 }
			@.mdl_vct = _arr◂mdl_k
			@.reg_vct = _arr◂reg_k
			@.etr_vct = _arr◂etr_k
			@.cst_vct = _arr◂cst_k
			@.type_vct = _arr◂var
			@.mdl_k = mdl_env
			@.reg_k = { _s8 _r64 _r64 } ## { name pos type-var } 
			@.etr_k
				∐ k_op : { ast.DP◂_r64 ast.lc_code◂_r64◂_r64 }
				∐ k_cst_v : ast.cst_v 
				∐ k_ctr : { _r64 _r64 }  
				∐ k_cst_def : ast.lc_code◂_r64◂_r64
			@.cst_k = _r64
			@.type_k
				∐ rcd : _lst◂type_k
				∐ dta : { _r64 _lst◂type_k }
				∐ imp : { type_k type_k }
				∐ ivar : _r64 
				∐ var : _r64
			@.type_exp 
				∐ app : { type_exp type_exp }
				∐ idx : _r64
			@.var
				∐ var_l : _r64
				∐ var_q : _r64 
				∐ var_ln : type_k

		§ @.occurs { tv_n tv i0 lv0 y1 } 
			∐ y1 ⊢ rcd◂l1 . 
				occurs_lst { tv_n tv i0 lv0 l1 } ⊢| 
			∐ y1 ⊢ dta◂{ a1 l1 } . 
				occurs_lst { tv_n tv i0 lv0 l1 } ⊢| 
			∐ y1 ⊢ imp◂{ y1_0 y1_1 } . 
				occurs { tv_n tv i0 lv0 y1_0 } ⊢ { tv_n tv i0 lv0 } 
				occurs { tv_n tv i0 lv0 y1_1 } ⊢ { tv_n tv i0 lv0 } 
				∎ { tv_n tv i0 lv0 } 
			∐ y1 ⊢ var◂i1 . 
				∐ i0'=i1' . 
					_emt { tv_n tv i0 lv0 } ⊢ { tv_n tv i0 lv0 } 
					∎| 
				∐. . 
					_lod_q { tv i1 } ⊢ { tv i1 v1 } 
					$ v1 ⊢ 
					∐ var_ln◂y2 . 
						occurs { tv_n tv i0 lv0 y2 } ⊢| 
					∐ var_l◂lv1 . 
						_setge { lv0 lv1 } ⊢ { lv0 lv1 b }
						∐ b'=0r . 
							$ lv0 ⊢ lv0,lv0_0 
							_get_q { tv i1 } ⊢ { tv i1 vt } 
							var_l lv0_0 ⊢ v2 
							_set_q { tv i1 v2 } ⊢ { tv i1 } 
							∎ { tv_n tv i0 lv0 } 
						∐. . 
							∎ { tv_n tv i0 lv0 }
					∐. var_q◂lv1 .
						_emt { i1 lv1 } ⊢ a 
						∎| 
			∐. . 
				_emt { tv_n tv i0 lv0 } ⊢ { tv_n tv i0 lv0 } 
				∎|
			@.occurs_lst { tv_n tv i0 lv0 l1 } 
				$ l1 ⊢ 
				∐ _cns◂{ y1 l1 } . 
					occurs { tv_n tv i0 lv0 y1 } ⊢ { tv_n tv i0 lv0 } 
					occurs_lst { tv_n tv i0 lv0 l1 } ⊢| 
				∐. _nil◂{} . 
					∎ { tv_n tv i0 lv0 } 
		§§ unify 
		§ @.f { tv_n tv y0 y1 }
				∐ y0 ⊢ var◂i0 . 
					f_l0 { tv_n tv i0 y1 } ⊢| 
				∐ y1 ⊢ var◂i1 . 
					f_l0 { tv_n tv i1 y0 } ⊢| 
				∐ y0 ⊢ rcd◂r0 ; y1 ⊢ rcd◂r1 . 
					f_lst { tv_n tv r0 r1 } ⊢| 
				∐ y0 ⊢ dta◂{ a0 l0 }; y1 ⊢ dta◂{ a1 l1 } . 
					∐ a0'=a1' . 
						f_lst { tv_n tv l0 l1 } ⊢|
					∐. . 
						∎| 
				∐ y0 ⊢ imp◂{ y0_0 y0_1 }; y1 ⊢ imp◂{ y1_0 y1_1 } . 
					f { tv_n tv y0_0 y1_0 } ⊢ { tv_n tv } 
					f { tv_n tv y0_1 y1_1 } ⊢|
				∐. . 
					_emt { y0 y1 } ⊢ { y0 y1 } 
					∎|
			@.f_lst { tv_n tv l0 l1 }
				∐ l0 ⊢ _cns◂{ y0 l0 }; l1 ⊢ _cns◂{ y1 l1 } . 
					f { tv_n tv y0 y1 } ⊢ { tv_n tv } 
					f_lst { tv_n tv l0 l1 } ⊢| 
				∐ l0 ⊢ _nil◂{}; l1 ⊢ _nil◂{} . 
					∎ { tv_n tv } 
				∐. . 
					∎|  
				
			@.f_l0 { tv_n tv i0 y1 }   
				_lod_q { tv i0 } ⊢ { tv i0 v0 } 
				∐ v0 ⊢ var_ln◂y2 . 
					f { tv_n tv y2 y1 } ⊢|
				∐ v0 ⊢ var_l◂lv .  
					f_l1 { tv_n tv i0 lv y1 } ⊢|
				∐. . 
					∎|  
			@.f_l1 { tv_n tv i0 lv y1 } 
				∐ y1 ⊢ var◂i1 . 
					∐ i0'=i1' . 
						∎ { tv_n tv } 
					∐. .
						_lod_q { tv i1 } ⊢ { tv i1 v1 }
						∐ v1 ⊢ var_ln◂y2 . 
							f_l1 { tv_n tv i0 lv y2 } ⊢|  
						∐ v1 ⊢ var_l◂lv1 . 
							_setge { lv lv1 } ⊢ { lv lv1 b }
							∐ b'=0r .  
								_get_q { tv i1 } ⊢ { tv i1 vt } 
								var i0 ⊢ yv 
								var_ln yv ⊢ v2 
								_set_q { tv i1 v2 } ⊢ { tv i1 } 
								∎ { tv_n tv } 
							∐. . 
								_get_q { tv i0 } ⊢ { tv i0 vt } 
								var i1 ⊢ yv 
								var_ln yv ⊢ v2 
								_set_q { tv i0 v2 } ⊢ { tv i0 } 
								∎ { tv_n tv }
						∐. . 
							∎|  
				∐. .  
					$ y1 ⊢ y1,y1_0 
					occurs { tv_n tv i0 lv y1 } ⊢ { tv_n tv i0 lv }
					_get_q { tv i0 } ⊢ { tv i0 vt }
					var_ln y1_0 ⊢ vy 
					_set_q { tv i0 vy } ⊢ { tv i0 }  
					∎ { tv_n tv }
			∎∎ 
		§ @.inst { tv_n tv lv y al } 
			$ y ⊢
			∐ rcd◂l . 
				inst_lst { tv_n tv lv l al } ⊢ { tv_n tv lv l al } 
				rcd l ⊢ y 
				∎ { tv_n tv lv y al } 
			∐ dta◂{ a l } . 
				inst_lst { tv_n tv lv l al } ⊢ { tv_n tv lv l al }
				dta { a l } ⊢ y 
				∎ { tv_n tv lv y al } 
			∐ imp◂{ y0 y1 } . 
				inst { tv_n tv lv y0 al } ⊢ { tv_n tv lv y0 al } 
				inst { tv_n tv lv y0 al } ⊢| 
			∐ var◂i0 . 
				_get_q { tv i0 } ⊢ { tv i0 v0 } 
				∐ v0 ⊢ var_ln◂y0 . 
					inst { tv_n tv lv y0 al } ⊢|
				∐ v0 ⊢ var_q◂lvq .
					assoc { al i0 } ⊢ { al iq o } 
					$ o ⊢ 
					∐ _some◂i0 . 
						var i0 ⊢ y0 
						∎ { tv_n tv lv y0 al } 
					∐. _none◂{} . 
						$ tv_n ⊢ tv_n,tv_n_0
						$ tv_n ⊢ tv_n,tv_n_1 
						_inc tv_n ⊢ tv_n
						$ lv ⊢ lv,lv_0 
						var_l lv_0 ⊢ v0
						_set_q { tv tv_n_0 v0 } ⊢ { tv tv_n_0 } 
						var tv_n_0 ⊢ y0 
						_some tv_n_1 ⊢ o 
						_cns { { iq o } al } ⊢ al  
						∎ { tv_n tv lv y0 al }  
				∐. . 
					var i0 ⊢ y 
					∎ { tv_n tv lv y al } 
			∐. ivar◂i . 
				∎| 
		@.inst_lst { tv_n tv lv l al } 
			$ l ⊢ 
			∐ _cns◂{ y l } . 
				inst { tv_n tv lv y al } ⊢ { tv_n tv lv y al } 
				inst_lst { tv_n tv lv l al } ⊢ { tv_n tv lv l al } 
				_cns { y l } ⊢ l 
				∎ { tv_n tv lv l al }
			∐. _nil◂{} . 
				_nil {} ⊢ l
				∎ { tv_n tv lv l al }
		§ @.gen { tv_n tv lv y } 
			$ y ⊢ 
			∐ rcd◂l . 
				gen_lst { tv_n tv lv l } ⊢ { tv_n tv lv l }
				rcd l ⊢ y 
				∎ { tv_n tv lv y } 
			∐ dta◂{ a l } . 
				gen_lst { tv_n tv lv l } ⊢ { tv_n tv lv l }
				dta { a l } ⊢ y
				∎ { tv_n tv lv y }
			∐ imp◂{ y0 y1 } . 
				gen { tv_n tv lv y0 } ⊢ { tv_n tv lv y0 } 
				gen { tv_n tv lv y1 } ⊢ { tv_n tv lv y1 } 
				imp { y0 y1 } ⊢ y 
				∎ { tv_n tv lv y } 
			∐ ivar◂i . 
				_emt { tv_n tv lv } ⊢ a 
				∎| 
			∐. var◂i . 
				_lod_q { tv i } ⊢ { tv i v } 
				∐ v ⊢ var_l◂lv1 .
					_setge { lv lv1 } ⊢ { lv lv1 b } 
					∐ b'=0r .
						_get_q { tv i } ⊢ { tv i vt } 
						$ lv ⊢ lv,lv_0 
						var_q lv_0 ⊢ vq 
						_set_q { tv i vq } ⊢ { tv i } 
						var i ⊢ y  
						∎ { tv_n tv lv y }
					∐. . 
						var i ⊢ y 
						∎ { tv_n tv lv y }
				∐ v ⊢ var_ln◂y1 . 
						gen { tv_n tv lv y1 } ⊢|  
				∐. . 
					_emt { tv i } ⊢ a 
					∎|
			@.gen_lst { tv_n tv lv l } 
				$ l ⊢ 
				∐ _cns◂{ y l } .
					gen { tv_n tv lv y } ⊢ { tv_n tv lv y } 
					gen_lst { tv_n tv lv l } ⊢ { tv_n tv lv l } 
					_cns { y l } ⊢ l 
					∎ { tv_n tv lv l } 
				∐. _nil◂{} . 
					_nil {} ⊢ l 
					∎ { tv_n tv lv l } 
		§ @.slv_ns_e { n mmv mev ev m_ns e_ns m_l } 
			$ n ⊢ 
			∐ ast.mdl_n◂{ w n } . 
				assoc { m_ns w } ⊢ { m_ns w o }
				$ o ⊢ 
				∐ _some◂i_m . 
					_lod_q { mmv i_m } ⊢ { mmv i_m m_ns0 }
					_lod_q { mev i_m } ⊢ { mev i_m e_ns0 }
					_nil {} ⊢ m_l0 
					slv_ns_e { n mmv mev ev m_ns0 e_ns0 m_l0 } ⊢ { mmv mev ev m_ns0 e_ns0 m_l0 i_e }
					∎ { mmv mev ev m_ns e_ns m_l i_e }
				∐. _none◂{} . 
					$ m_l ⊢ 
					∐ _cns◂{ i_m_r m_l } .
						_lod_q { mmv i_m_r } ⊢ { mmv i_m_r m_ns0 }
						_lod_q { mev i_m_r } ⊢ { mev i_m_r e_ns0 }
						ast.mdl_n { w n } ⊢ n 
						slv_ns_e { n mmv mev ev m_ns0 e_ns0 m_l } ⊢ { mmv mev ev m_ns0 e_ns0 m_l i_e }
						_cns { i_m_r m_l } ⊢ m_l 
						∎ { mmv mev ev m_ns e_ns m_l i_e }
					∐. _nil◂{} . 
						» _^ _ ⊢ sr 
							"slv_ns_e 0:not found\n" //
						_emt sr ⊢ sr 
						_emt { m_ns w } ⊢ { m_ns w }
						∎|  
			∐. ast.fst_n◂w . 
				assoc { e_ns w } ⊢ { e_ns w o } 
				$ o ⊢
				∐ _some◂i_e . 
					∎ { mmv mev ev m_ns e_ns m_l i_e }
				∐. _none◂{} . 
					$ m_l ⊢ 
					∐ _cns◂{ i_m_r m_l } .
						_lod_q { mmv i_m_r } ⊢ { mmv i_m_r m_ns0 }
						_lod_q { mev i_m_r } ⊢ { mev i_m_r e_ns0 }
						ast.fst_n w ⊢ n 
						slv_ns_e { n mmv mev ev m_ns0 e_ns0 m_l } ⊢ { mmv mev ev m_ns0 e_ns0 m_l i_e }
						_cns { i_m_r m_l } ⊢ m_l 
						∎ { mmv mev ev m_ns e_ns m_l i_e }
					∐. _nil◂{} . 
						_emt { m_ns w } ⊢ { m_ns w }
						» _^ _ ⊢ sr 
							"slv_ns_m 1:not found\n" //
						_emt sr ⊢ sr 
						∎|  
		§ @.slv_ns_m { n mmv m_ns m_l } 
			$ n ⊢ 
			∐ ast.mdl_n◂{ w n } . 
				assoc { m_ns w } ⊢ { m_ns w o }
				$ o ⊢ 
				∐ _some◂i_m0 . 
					_lod_q { mmv i_m0 } ⊢ { mmv i_m0 m_ns0 }
					_nil {} ⊢ m_l0 
					slv_ns_m { n mmv m_ns0 m_l0 } ⊢ { mmv m_ns0 m_l0 i_m }
					∎ { mmv m_ns m_l i_m }
				∐. _none◂{} . 
					$ m_l ⊢ 
					∐ _cns◂{ i_m_r m_l } .
						_lod_q { mmv i_m_r } ⊢ { mmv i_m_r m_ns0 }
						ast.mdl_n { w n } ⊢ n 
						slv_ns_m { n mmv m_ns0 m_l } ⊢ { mmv m_ns0 m_l i_m }
						_cns { i_m_r m_l } ⊢ m_l 
						∎ { mmv m_ns m_l i_m }
					∐. _nil◂{} . 
						» _^ _ ⊢ sr 
							"slv_ns_m 0:not found\n" //
						_emt sr ⊢ sr 
						_emt { m_ns w } ⊢ { m_ns w }
						∎|    
			∐. ast.fst_n◂w . 
				assoc { m_ns w } ⊢ { m_ns w o } 
				$ o ⊢
				∐ _some◂i_m . 
					∎ { mmv m_ns m_l i_m }
				∐. _none◂{} . 
					$ m_l ⊢ 
					∐ _cns◂{ i_m_r m_l } .
						_lod_q { mmv i_m_r } ⊢ { mmv i_m_r m_ns0 }
						ast.fst_n w ⊢ n 
						slv_ns_m { n mmv m_ns0 m_l } ⊢ { mmv m_ns0 m_l i_m }
						_cns { i_m_r m_l } ⊢ m_l 
						∎ { mmv m_ns m_l i_m }
					∐. _nil◂{} . 
						» _^ _ ⊢ sr 
							"slv_ns_m 1:not found\n" //
						_emt sr ⊢ sr 
						_emt { m_ns w } ⊢ { m_ns w }
						∎|
	
		§ slv_cst { env0 c } 
			$ c ⊢ 
			∐ ast.cst_name◂cn . 
				$ env0 ⊢ { mmv mev ev m_ns e_ns m_l } 
				slv_ns_e { cn mmv mev ev m_ns e_ns m_l } ⊢ { mmv mev ev m_ns e_ns m_l i_e }
				$ { mmv mev ev m_ns e_ns m_l } ⊢ env0 	
				ast.cst_name i_e ⊢ c 
				∎ { env0 c }
			∐. ast.cst_v◂v . 
				ast.cst_v v ⊢ c 
				∎ { env0 c } 
		§ @.crt_mtc_ptn { env0 n re p }
				_emt { n re p } ⊢ { n re p }
				∐ p ⊢ ast.RP.rcd◂l .
					crt_mtc_ptn_lst { env0 n re l } ⊢ { env0 n re l }
					ast.RP.rcd l ⊢ p
					∎ { env0 n re p }
				∐. p ⊢ ast.RP.atm◂a .
				 	$ a ⊢ 
					∐ ast.mtc_reg◂w .
						_emt { w n } ⊢ { w n }
						assoc { re w } ⊢ { re w o }
						∐ o ⊢ _none◂{} .
							$ n ⊢ n,n0
							_some n0 ⊢ o
							_cns { { w o } re } ⊢ re
							$ n ⊢ n,n0
							ast.mtc_reg n0 ⊢ a
							_inc n ⊢ n
							ast.RP.atm a ⊢ p
							∎ { env0 n re p }
						∐. .
							∎|
					∐ ast.mtc_cst◂c . 
						slv_cst { env0 c } ⊢ { env0 c } 
						ast.mtc_cst c ⊢ a 
						ast.RP.atm a ⊢ p 
						∎ { env0 n re p }
					∐ ast.mtc_agl◂{ cn ap } . 
						$ env0 ⊢ { mmv mev ev m_ns e_ns m_l } 
						slv_ns_e { cn mmv mev ev m_ns e_ns m_l } ⊢ { mmv mev ev m_ns e_ns m_l i_e } 
						$ { mmv mev ev m_ns e_ns m_l } ⊢ env0 
						crt_mtc_ptn { env0 n re ap } ⊢ { env0 n re ap } 
						ast.mtc_agl { i_e ap } ⊢ a
						ast.RP.atm a ⊢ p 
						∎ { env0 n re p }
					∐. ast.mtc_wc◂{} .
						ast.mtc_wc {} ⊢ a
						ast.RP.atm a ⊢ p
						∎ { env0 n re p }
			@.crt_mtc_ptn_lst { env0 n re l }
				∐ l ⊢ _cns◂{ hd l } .
					crt_mtc_ptn { env0 n re hd } ⊢ { env0 n re hd }
					crt_mtc_ptn_lst { env0 n re l } ⊢ { env0 n re l }
					_cns { hd l } ⊢ l
					∎ { env0 n re l }
				∐. .
					_nil {} ⊢ l0
					∎ { env0 n re l0 }
		§ @.crt_dst_ptn { n re p }
				_emt { n re p } ⊢ { n re p }
				∐ p ⊢ ast.RP.rcd◂l .
					crt_dst_ptn_lst { n re l } ⊢ { n re l }
					ast.RP.rcd l ⊢ p
					∎ { n re p }
				∐. p ⊢ ast.RP.atm◂a .
					crt_dst { n re a } ⊢ { n re a } 
					ast.RP.atm a ⊢ p
					∎ { n re p }
			@.crt_dst { n re a } 
				 	∐ a ⊢ ast.dst_reg◂w .
						_emt { w n } ⊢ { w n }
						assoc { re w } ⊢ { re w o }
						∐ o ⊢ _none◂{} .
							$ n ⊢ n,n0
							_some n0 ⊢ o
							_cns { { w o } re } ⊢ re
							$ n ⊢ n,n0
							_inc n ⊢ n
							ast.dst_reg n0 ⊢ a
							∎ { n re a }
						∐. .
							∎|
					∐. a ⊢ ast.wc◂{} .
						ast.wc {} ⊢ a
						∎ { n re a }
			@.crt_dst_ptn_lst { n re l }
				∐ l ⊢ _cns◂{ hd l } .
					crt_dst_ptn { n re hd } ⊢ { n re hd }
					crt_dst_ptn_lst { n re l } ⊢ { n re l }
					_cns { hd l } ⊢ l
					∎ { n re l }
				∐. .
					_nil {} ⊢ l0
					∎ { n re l0 }
			@.crt_dst_lst { n re l }
				∐ l ⊢ _cns◂{ hd l } .
					crt_dst { n re hd } ⊢ { n re hd }
					crt_dst_lst { n re l } ⊢ { n re l }
					_cns { hd l } ⊢ l
					∎ { n re l }
				∐. .
					_nil {} ⊢ l0
					∎ { n re l0 }
		§ @.csm_src_ptn { re p }
				∐ p ⊢ ast.RP.rcd◂l .
					csm_src_ptn_lst { re l } ⊢ { re l }
					ast.RP.rcd l ⊢ p
					∎ { re p }
				∐. p ⊢ ast.RP.atm◂a .
					csm_src { re a } ⊢ { re a } 
					ast.RP.atm a ⊢ p 
					∎ { re p }
			@.csm_src { re a }
				∐ a ⊢ ast.src_reg◂w .
					assoc { re w } ⊢ { re w o }
					∐ o ⊢ _some◂i .
						ast.src_reg i ⊢ a
						_none {} ⊢ o
						_cns { { w o } re } ⊢ re
						∎ { re a }
					∐. .
						_emt { re w } ⊢ { re w }
						∎|
				∐. a ⊢ ast.im_reg◂c .
					ast.im_reg c ⊢ a
					∎ { re a }
			@.csm_src_ptn_lst { re l }
				∐ l ⊢ _cns◂{ hd l } .
					csm_src_ptn { re hd } ⊢ { re hd }
					csm_src_ptn_lst { re l } ⊢ { re l }
					_cns { hd l } ⊢ l
					∎ { re l }
				∐. .
					_nil {} ⊢ l0
					∎ { re l0 }
			@.csm_src_lst { re l }
				∐ l ⊢ _cns◂{ hd l } .
					csm_src { re hd } ⊢ { re hd }
					csm_src_lst { re l } ⊢ { re l }
					_cns { hd l } ⊢ l
					∎ { re l }
				∐. .
					_nil {} ⊢ l0
					∎ { re l0 }
		§ @.slv_src_ptn { re p }
				∐ p ⊢ ast.RP.rcd◂l .
					slv_src_ptn_lst { re l } ⊢ { re l }
					ast.RP.rcd l ⊢ p
					∎ { re p }
				∐. p ⊢ ast.RP.atm◂a .
					slv_src { re a } ⊢ { re a } 
					ast.RP.atm a ⊢ p 
					∎ { re p }
			@.slv_src { re a }
				∐ a ⊢ ast.src_reg◂w .
					assoc { re w } ⊢ { re w o }
					∐ o ⊢ _some◂i .
						ast.src_reg i ⊢ a
						_none {} ⊢ o
						∎ { re a }
					∐. .
						_emt { re w } ⊢ { re w } 
						∎|
				∐. a ⊢ ast.im_reg◂c .
					ast.im_reg c ⊢ a
					∎ { re a }
			@.slv_src_ptn_lst { re l }
				∐ l ⊢ _cns◂{ hd l } .
					slv_src_ptn { re hd } ⊢ { re hd }
					slv_src_ptn_lst { re l } ⊢ { re l }
					_cns { hd l } ⊢ l
					∎ { re l }
				∐. .
					_nil {} ⊢ l0
					∎ { re l0 }
		§ @.slv_test_var { env0 n re v } 
			$ v ⊢ 
			∐ ast.test_var◂w . 
				assoc { re w } ⊢ { re w o } 
				$ o ⊢ 
				∐ _some◂i . 
					ast.test_var i ⊢ v 
					∎ { env0 n re v }
				∐. _none◂{} . 
					∎| 
			∐. ast.test_cst◂c . 
				slv_cst { env0 c } ⊢ { env0 c }
				ast.test_cst c ⊢ v 
				∎ { env0 n re v }
		§ slv_exp_s8 { env0 e } 
			$ e ⊢ 
			∐ ast.exp_s8_cst◂c . 
				slv_cst { env0 c } ⊢ { env0 c } 
				ast.exp_s8_cst c ⊢ e 
				∎ { env0 e }
			∐ ast.exp_s8_s8◂s . 
				ast.exp_s8_s8 s ⊢ e 
				∎ { env0 e } 
			∐ ast.exp_s8_idx◂p . 
				ast.exp_s8_idx p ⊢ e 
				∎ { env0 e } 
			∐. ast.exp_s8_for◂p . 
				ast.exp_s8_for p ⊢ e 
				∎ { env0 e } 
		§ @.slv_exp_s8_lst { env0 el } 
			$ el ⊢ 
			∐ _cns◂{ hd el } .
				slv_exp_s8 { env0 hd } ⊢ { env0 e } 
				slv_exp_s8_lst { env0 el } ⊢ { env0 el }
				_cns { e el } ⊢ el 
				∎ { env0 el }
			∐. _nil◂{} . 
				_nil {} ⊢ el 
				∎ { env0 el } 
		§ @.slv_r { env0 n re lc }
			$ lc ⊢
			∐ ast.jmp◂{ en x } . 
				$ env0 ⊢ { mmv mev ev m_ns e_ns m_l } 
				slv_ns_e { en mmv mev ev m_ns e_ns m_l } ⊢ { mmv mev ev m_ns e_ns m_l i_e } 
				$ { mmv mev ev m_ns e_ns m_l } ⊢ env0 
				csm_src_ptn { re x } ⊢ { re x } 
				ast.jmp { i_e x } ⊢ lc 
				∎ { env0 n re lc } 
			∐ ast.app_jmp◂{ f x } . 
				csm_src { re f } ⊢ { re f } 
				csm_src_ptn { re x } ⊢ { re x } 
				ast.app_jmp { f x } ⊢ lc 
				∎ { env0 n re lc } 
			∐ ast.seq◂{ ll lc } .
				$ ll ⊢
				∐ ast.op_name◂{ o s d } .
					$ env0 ⊢ { mmv mev ev m_ns e_ns m_l } 
					slv_ns_e { o mmv mev ev m_ns e_ns m_l } ⊢ { mmv mev ev m_ns e_ns m_l i_e } 
					$ { mmv mev ev m_ns e_ns m_l } ⊢ env0 
					csm_src_ptn { re s } ⊢ { re s }
					crt_dst_ptn { n re d } ⊢ { n re d }
					ast.op_name { i_e s d } ⊢ ll
					slv_r { env0 n re lc } ⊢ { env0 n re lc }
					ast.seq { ll lc } ⊢ lc
					∎ { env0 n re lc }
				∐ ast.op_app◂{ f x y } .
					csm_src { re f } ⊢ { re f } 
					csm_src_ptn { re x } ⊢ { re x } 
					crt_dst_ptn { n re y } ⊢ { n re y } 
					ast.op_app { f x y } ⊢ ll 
					slv_r { env0 n re lc } ⊢ { env0 n re lc }
					ast.seq { ll lc } ⊢ lc 
					∎ { env0 n re lc } 
				∐ ast.op_exp_cst◂{ c s d } .
					slv_cst { env0 c } ⊢ { env0 c } 
					csm_src_ptn { re s } ⊢ { re s } 
					crt_dst_ptn { n re d } ⊢ { n re d } 
					ast.op_exp_cst { c s d } ⊢ ll 
					slv_r { env0 n re lc } ⊢ { env0 n re lc }
					ast.seq { ll lc } ⊢ lc 
					∎ { env0 n re lc }
				∐. ast.op_exp_s8◂{ pl ss ds } .
					slv_exp_s8_lst { env0 pl } ⊢ { env0 pl0 } 
					##_nil {} ⊢ pl0 
					csm_src_lst { re ss } ⊢ { re ss } 
					crt_dst_lst { n re ds } ⊢ { n re ds } 
					ast.op_exp_s8 { pl0 ss ds } ⊢ ll 
					slv_r { env0 n re lc } ⊢ { env0 n re lc } 
					ast.seq { ll lc } ⊢ lc 
					∎ { env0 n re lc }
			∐ ast.mtc◂lm .
				slv_lc_mtc { env0 n re lm } ⊢ { env0 n re lm }
				ast.mtc lm ⊢ lc 
				∎ { env0 n re lc } 
			∐ ast.op_id◂{ sp ll } . 
				$ ll ⊢ 
				∐ ast.id_mtc◂ilm . 
					csm_src_ptn { re sp } ⊢ { re sp } 
					slv_lc_id_mtc { env0 n re ilm } ⊢ { env0 n re ilm } 
					ast.id_mtc ilm ⊢ ll 
					ast.op_id { sp ll } ⊢ lc 
					∎ { env0 n re lc } 
				∐. ast.id_line◂{ dl lc } . 
					csm_src_ptn { re sp } ⊢ { re sp } 
					crt_dst_ptn_lst { n re dl } ⊢ { n re dl }
					slv_r { env0 n re lc } ⊢ { env0 n re lc } 
					ast.id_line { dl lc } ⊢ ll
					ast.op_id { sp ll } ⊢ lc 
					∎ { env0 n re lc }
			∐ ast.ret◂s .
				csm_src_ptn { re s } ⊢ { re s } 
				ast.ret s ⊢ lc 
				∎ { env0 n re lc } 			
			∐. ast.exn◂{} .
				ast.exn {} ⊢ lc 
				∎ { env0 n re lc }   			
		@.slv_lc_mtc { env0 n re lm } 
			$ lm ⊢ 
			∐ p_cns◂{ { tl me } lm } .  
				$ re ⊢ re,re0 
				slv_lc_mtc { env0 n re0 lm } ⊢ { env0 n re0 lm } 
				slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
				slv_mtc_etr { env0 n re me } ⊢ { env0 n re me } 
				p_cns { { tl me } lm } ⊢ lm 
				∎ { env0 n re lm }
			∐. p_end◂{ tl me } .
				slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
				slv_mtc_etr { env0 n re me } ⊢ { env0 n re me } 
				p_end { tl me } ⊢ lm 
				∎ { env0 n re lm }
		@.slv_mtc_etr { env0 n re me } 
			$ me ⊢ 
			∐ ast.mtc_rmf◂lm0 .
				slv_lc_mtc { env0 n re lm0 } ⊢ { env0 n re lm0 }  
				ast.mtc_rmf lm0 ⊢ me 
				∎ { env0 n re me } 
			∐ ast.mtc_id_rmf◂{ sp0 ilm0 } .  
				csm_src_ptn { re sp0 } ⊢ { re sp0 } 
				slv_lc_id_mtc { env0 n re ilm0 } ⊢ { env0 n re ilm0 } 
				ast.mtc_id_rmf { sp0 ilm0 } ⊢ me  
				∎ { env0 n re me }  
			∐. ast.mtc_code◂lc0 . 
				slv_r { env0 n re lc0 } ⊢ { env0 n re lc0 } 
				ast.mtc_code lc0 ⊢ me 
				∎ { env0 n re me } 
		@.slv_mtc_line { env0 n re tl } 
			$ tl ⊢ 
			∐ _cns◂{ t tl } . 
				$ t ⊢ 
				∐ ast.mtc_id◂{ sp mp } . 
					csm_src_ptn { re sp } ⊢ { re sp } 
					crt_mtc_ptn { env0 n re mp } ⊢ { env0 n re mp }
					ast.mtc_id { sp mp } ⊢ t 
					slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
					_cns { t tl } ⊢ tl 
					∎ { env0 n re tl } 
				∐. ast.mtc_test◂{ v0 v1 } .  
					slv_test_var { env0 n re v0 } ⊢ { env0 n re v0 } 
					slv_test_var { env0 n re v1 } ⊢ { env0 n re v1 } 
					ast.mtc_test { v0 v1 } ⊢ t 
					slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
					_cns { t tl } ⊢ tl 
					∎ { env0 n re tl } 
			∐. _nil◂{} . 
				_nil {} ⊢ tl 
				∎ { env0 n re tl }
		@.slv_lc_id_mtc { env0 n re ilm } 
			$ ilm ⊢ 
			∐ p_cns◂{ { mp tl me } ilm } .  
				$ re ⊢ re,re0 
				slv_lc_id_mtc { env0 n re0 ilm } ⊢ { env0 n re0 ilm } 
				crt_mtc_ptn { env0 n re mp } ⊢ { env0 n re mp } 
				slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
				slv_mtc_etr { env0 n re me } ⊢ { env0 n re me } 
				p_cns { { mp tl me } ilm } ⊢ ilm 
				∎ { env0 n re ilm }
			∐. p_end◂{ mp tl me } .
				crt_mtc_ptn { env0 n re mp } ⊢ { env0 n re mp } 
				slv_mtc_line { env0 n re tl } ⊢ { env0 n re tl } 
				slv_mtc_etr { env0 n re me } ⊢ { env0 n re me } 
				p_end { mp tl me } ⊢ ilm 
				∎ { env0 n re ilm }
		§ add_e { n ns e } 
			$ n ⊢ n,n0 
			_inc n ⊢ n 
			_some n0 ⊢ o 
			_cns { { e o } ns } ⊢ ns 
			∎ { n ns } 
		§§ slv_lc_clq 
			§ 
				@.f { env0 ev_n r_n q } 
					_nil {} ⊢ cl 
					f_l0 { env0 ev_n r_n q cl } ⊢ { env0 ev_n r_n q } 
					f_l1 { env0 r_n q } ⊢ { env0 r_n q } 
					∎ { env0 ev_n r_n q } 
				@.f_l0 { env0 ev_n r_n q cl }
					$ q ⊢ 
					∐ _cns◂{ { w d lc } q } . 
						assoc { cl w } ⊢ { cl w o } 
						$ o ⊢ 
						∐ _none◂{} . 
							$ w ⊢ w,w0 
							_some {} ⊢ a 
							_cns { { w0 a } cl } ⊢ cl 
							$ env0 ⊢ { mmv mev ev ns_m ns_e il }
							add_e { ev_n ns_e w } ⊢ { ev_n ns_e }
							$ { mmv mev ev ns_m ns_e il } ⊢ env0 
							f_l0 { env0 ev_n r_n q cl } ⊢ { env0 ev_n r_n q } 
							_cns { { d lc } q } ⊢ q 
							∎ { env0 ev_n r_n q }
						∐. _some◂{} . 
							_emt w ⊢ w 
							∎| 
					∐. _nil◂{} . 
						_nil {} ⊢ q 
						∎ { env0 ev_n r_n q } 
				@.f_l1 { env0 r_n q } 
					$ q ⊢ 
					∐ _cns◂{ { d lc } q } . 
						_nil {} ⊢ re 
						crt_dst_ptn { r_n re d } ⊢ { r_n re d } 
						slv_r { env0 r_n re lc } ⊢ { env0 r_n re lc } 
						f_l1 { env0 r_n q } ⊢ { env0 r_n q } 
						_cns { { d lc } q } ⊢ q 
						∎ { env0 r_n q } 
					∐. _nil◂{} . 
						_nil {} ⊢ q 
						∎ { env0 r_n q }
			∎∎ 
		§ @.slv_dt_def { ev ev_n ns_e dl } 
			$ dl ⊢ 
			∐ _cns◂{ { cn y } dl } . 
				add_e { ev_n ns_e cn } ⊢ { ev_n ns_e } 
				slv_dt_def { ev ev_n ns_e dl } ⊢| 
			∐. _nil◂{} . 
				∎ { ev ev_n ns_e } 
		§ slv_dt_etr { ev ev_n ns_e e } 
			$ e ⊢ 
			∐ ast.dt_def◂dl .
				slv_dt_def { ev ev_n ns_e dl } ⊢| 
			∐. ast.dt_ln◂y . 
				∎ { ev ev_n ns_e }
		§ @.slv_dt_clq { ev ev_n ns_e q } 
			$ q ⊢ 
			∐ _cns◂{ { w d } q } . 
				slv_dt_etr { ev ev_n ns_e d } ⊢ { ev ev_n ns_e } 
				slv_dt_clq { ev ev_n ns_e q } ⊢|
			∐. _nil◂{} .
				∎ { ev ev_n ns_e } 
		§ @.slv_grm { mmv mev mtv ns_m mv_n ev ev_n g } 
			$ g ⊢ 
			∐ _cns◂{ { w e } g } . 
				$ mv_n ⊢ mv_n,mv_n_0 
				_inc mv_n ⊢ mv_n  
				_nil {} ⊢ ns_m_g 
				_nil {} ⊢ ns_e_g 
				_nil {} ⊢ ns_t_g
				» "prs" _ ⊢ e0 
				add_e { ev_n ns_e_g e0 } ⊢ { ev_n ns_e_g }
				_set_q { mmv mv_n_0 ns_m_g } ⊢ { mmv mv_n_0 }
				_set_q { mev mv_n_0 ns_e_g } ⊢ { mev mv_n_0 }
				_set_q { mtv mv_n_0 ns_t_g } ⊢ { mtv mv_n_0 }				
				_some mv_n_0 ⊢ o 
				_cns { { w o } ns_m } ⊢ ns_m 
				slv_grm { mmv mev mtv ns_m mv_n ev ev_n g } ⊢|
			∐. _nil◂{} . 
				∎ { mmv mev mtv ns_m mv_n ev ev_n } 
		§
			@.slv_file gel
				_nil {} ⊢ ns_m 
				_nil {} ⊢ ns_t 
				_nil {} ⊢ ns_e 
				» 200r _ ⊢ max
				_mk_arr max ⊢ { max mmv }
				_mk_arr max ⊢ { max mev }
				_mk_arr max ⊢ { max mtv }
				_mk_arr max ⊢ { max rv }
				_mk_arr max ⊢ { max ev }
				_mk_arr max ⊢ { max tv }
				» 1r _ ⊢ mv_n
				» 0r _ ⊢ rv_n
				» 0r _ ⊢ ev_n
				» 0r _ ⊢ tv_n
				» 0r _ ⊢ i 
				_nil {} ⊢ il 
				» "_mk_arr" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_exc_q" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_set_q" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_get_q" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_lod_q" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_args" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_mlc_s8" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_emt" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_inc" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_dec" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_add" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_sts" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_lds" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e }
				» "_setge" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e }  
				» "_rep_movsb" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_add" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_nil" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_cns" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_some" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_none" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				» "_in_fn" _ ⊢ se
				add_e { ev_n ns_e se } ⊢ { ev_n ns_e } 
				slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢| 
			@.slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel }
				_emt ns_m ⊢ ns_m 
				$ gel ⊢ 
				∐ _cns◂{ e gel } .
					$ e ⊢ 
					∐ ast.mdl_etr◂{ w e } .
						$ e ⊢ 
						∐ ast.mdl_def◂gel0 . 
							$ mv_n ⊢ mv_n,mv_n_0 
							_inc mv_n ⊢ mv_n
							_set_q { mmv i ns_m } ⊢ { mmv i } 
							_set_q { mev i ns_e } ⊢ { mev i } 
							_set_q { mtv i ns_t } ⊢ { mtv i } 
							_cns { i il } ⊢ il 
							_nil {} ⊢ ns_m 
							_nil {} ⊢ ns_e 
							_nil {} ⊢ ns_t 
							slv_file_lp { mv_n_0 il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel0 } ⊢ { mv_n_0 il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t }
							_set_q { mmv mv_n_0 ns_m } ⊢ { mmv mv_n_0 } 
							_set_q { mev mv_n_0 ns_e } ⊢ { mev mv_n_0 } 
							_set_q { mtv mv_n_0 ns_t } ⊢ { mtv mv_n_0 } 
							$ il ⊢ 
							∐ _cns◂{ i il } . 
								_some mv_n_0 ⊢ o 
								_get_q { mmv i } ⊢ { mmv i ns_m }
								_get_q { mev i } ⊢ { mev i ns_e } 
								_get_q { mtv i } ⊢ { mtv i ns_t } 
								_cns { { w o } ns_m } ⊢ ns_m
								slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢| 
							∐. _nil◂{} . 
								∎| 
						∐. ast.mdl_ln◂n0 . 							
							$ n0 ⊢ n0,n0_0 
							slv_ns_m { n0 mmv ns_m il } ⊢ { mmv ns_m il i_m } 
							_some i_m ⊢ o 
							_cns { { w o } ns_m } ⊢ ns_m 
							slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
					∐ ast.grm_etr◂g .
						slv_grm { mmv mev mtv ns_m mv_n ev ev_n g } ⊢ { mmv mev mtv ns_m mv_n ev ev_n }
						slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
					∐ ast.lc_etr◂{ w e } .
						$ e ⊢
						∐ ast.lc_def◂{ d lc } .
							_nil {} ⊢ ns_r
							crt_dst_ptn { rv_n ns_r d } ⊢ { rv_n ns_r d } 
							slv_r { { mmv mev ev ns_m ns_e il } rv_n ns_r lc } ⊢ { { mmv mev ev ns_m ns_e il } rv_n ns_r lc } 
							add_e { ev_n ns_e w } ⊢ { ev_n ns_e }
							slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
						∐ ast.lc_ln◂c . 
							$ c ⊢ 
							∐ ast.cst_name◂cn . 
								$ cn ⊢ cn,cn0 
								slv_ns_e { cn0 mmv mev ev ns_m ns_e il } ⊢ { mmv mev ev ns_m ns_e il i_e } 
								_some i_e ⊢ o 
								_cns { { w o } ns_e } ⊢ ns_e 
								slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
							∐. ast.cst_v◂v . 
								add_e { ev_n ns_e w } ⊢ { ev_n ns_e } 
								slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
						∐. ast.lc_cst◂lc . 
							_nil {} ⊢ ns_r 
							slv_r { { mmv mev ev ns_m ns_e il } rv_n ns_r lc } ⊢ { { mmv mev ev ns_m ns_e il } rv_n ns_r lc } 
							add_e { ev_n ns_e w } ⊢ { ev_n ns_e } 
							slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
					∐ ast.lc_qlq_etr◂q .
						slv_lc_clq.f { { mmv mev ev ns_m ns_e il } ev_n rv_n q } ⊢ { { mmv mev ev ns_m ns_e il } ev_n rv_n q }
						slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
					∐ ast.dt_etr◂{ w e } .
						slv_dt_etr { ev ev_n ns_e e } ⊢ { ev ev_n ns_e } 
						slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
					∐. ast.dt_qlq_etr◂q .
						slv_dt_clq { ev ev_n ns_e q } ⊢ { ev ev_n ns_e }
						slv_file_lp { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t gel } ⊢|
				∐. _nil◂{} . 
					∎ { i il { mmv mev mtv mv_n rv rv_n ev ev_n tv tv_n } ns_m ns_e ns_t }		
		∎∎
	
	§ f0 «
		» _args _ ⊢ ags
		∐ ags ⊢ _cns◂{ pn tl0 }; tl0 ⊢ _cns◂{ fn0 tl1 }; tl1 ⊢ _nil◂{} .
			_in_fn fn0 ⊢ { fn0 fn0_c }
			» 0r _ ⊢ x1
			syntax.file.prs { fn0_c x1 } ⊢ { fn0_c x1 a0 }
			∐ a0 ⊢ _some◂a0 .
				$ a0 ⊢ 
				∐ syntax.prs_vld◂a0 . 
					_emt x1 ⊢ x1 
					ns.slv_file a0 ⊢ v 
					_emt v ⊢ v 
					\[
					» 0r _ ⊢ dn
					ast.pnt { dn a0 } ⊢ { dn sr }
					» _^ sr ⊢ sr,sr0
						"prs_vld:\n"
						0'
						 //
					_emt sr0 ⊢ sr0
					\]
					∎ {}
				∐. syntax.prs_err◂err . 
						syntax.pnt_prs_err err ⊢ s_err 
						_emt s_err ⊢ s_err 
						∎ {}
			∐. .
				» "prs_failed:\n" _ ⊢ sc0
				_emt sc0 ⊢ sc0
				∎ {}
		∐. .
			» "no argument\n:" _ ⊢ s
			_emt s ⊢ s 
			∎ {}			
∎∎
